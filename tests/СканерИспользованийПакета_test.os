#Использовать asserts
#Использовать autumn
#Использовать httpbin
#Использовать collectionos
#Использовать 1connector
#Использовать "../src/core"

Перем _HttpBin; // HttpBin
Перем _СканерИспользованийПакета; // СканерИспользованийПакета

&ТестовыйНабор(Характер = "Одиночка")
Процедура ПриСозданииОбъекта()
	
КонецПроцедуры

&ПередВсеми
Процедура ПередВсеми() Экспорт

	// HttpBin
	_HttpBin = Новый HttpBin(, 3434);
	_HttpBin.УстановитьРасположениеКонтроллеров("./tests/fixtures/api-mocks");
	_HttpBin.Запустить();
		
	// Autumn
	ЗначенияДеталек = Новый Соответствие();
	ЗначенияДеталек.Вставить("litros.АдресGitHubAPI", _HttpBin.URL());

	СоветДругогоМастера = Новый СоветДругогоМастера();
	СоветДругогоМастера.ЗначенияДеталек(ЗначенияДеталек);

	Поделка = Новый Поделка(СоветДругогоМастера);
	Поделка.ЗапуститьПриложение();

	_СканерИспользованийПакета = Поделка.НайтиЖелудь("СканерИспользованийПакета");

КонецПроцедуры

&ПередКаждым
Процедура ПередКаждым() Экспорт
	ОтключитьЛимит();
КонецПроцедуры

&ПослеВсех
Процедура ПослеВсех() Экспорт
	Если _HttpBin <> Неопределено Тогда
		_HttpBin.Остановить();
	КонецЕсли;
КонецПроцедуры

&Тест
Процедура ТестДолжен_НайтиРепозиторииБезЛимитов() Экспорт

	// Подготовка
	ФорматнаяСтрокаЧисла = "ЧН=0; ЧГ=";

	ТестовыеСлучаи = Новый Массив();
	ДобавитьТестовыйСлучай(ТестовыеСлучаи, 0, 0, 0);
	ДобавитьТестовыйСлучай(ТестовыеСлучаи, 5, 2, 0);
	ДобавитьТестовыйСлучай(ТестовыеСлучаи, 5, 0, 2);
	ДобавитьТестовыйСлучай(ТестовыеСлучаи, 5, 2, 1);
	ДобавитьТестовыйСлучай(ТестовыеСлучаи, 5, 5, 0);
	ДобавитьТестовыйСлучай(ТестовыеСлучаи, 150, 20, 30);
	ДобавитьТестовыйСлучай(ТестовыеСлучаи, 150, 20, 0);
	ДобавитьТестовыйСлучай(ТестовыеСлучаи, 150, 0, 30);
	ДобавитьТестовыйСлучай(ТестовыеСлучаи, 150, 150, 0);
	ДобавитьТестовыйСлучай(ТестовыеСлучаи, 1000, 50, 60);
	ДобавитьТестовыйСлучай(ТестовыеСлучаи, 1500, 30, 70);

	Для Каждого ТестовыйСлучай Из ТестовыеСлучаи Цикл

		ИмяПакета = СтрШаблон("totalCount_%1_repoCount_%2_forkCount_%3", 
			Формат(ТестовыйСлучай.КоличествоЭлементов, ФорматнаяСтрокаЧисла), 
			Формат(ТестовыйСлучай.КоличествоОсновныхРепозиториев, ФорматнаяСтрокаЧисла), 
			Формат(ТестовыйСлучай.КоличествоФорков, ФорматнаяСтрокаЧисла));

		// Действие
		Репозитории = _СканерИспользованийПакета.НайтиРепозитории(ИмяПакета);

		// Утверждение
		Ожидаем.Что(Репозитории).ИмеетТип("Массив");
		Ожидаем.Что(Репозитории).ИмеетДлину(ТестовыйСлучай.КоличествоОсновныхРепозиториев + ТестовыйСлучай.КоличествоФорков);
		
		Статистика = СтатистикаНайденнызРепозиториев(Репозитории);
		Ожидаем.Что(Статистика.КоличествоОсновныхРепозиториев, "Количество основных репозиториев").Равно(ТестовыйСлучай.КоличествоОсновныхРепозиториев);
		Ожидаем.Что(Статистика.КоличествоФорков, "Количество форков").Равно(ТестовыйСлучай.КоличествоФорков);

	КонецЦикла;

КонецПроцедуры

&Тест
Процедура ТестДолжен_ПроверитьСохранениеПромежуточныхРезультатов() Экспорт
	
	// Подготовка
	ИмяПакета = "totalCount_200_repoCount_150_forkCount_50";// 2 страницы по 100
	УстановитьЛимит(2); // 2 страницы основных репозиториев

	// Действие
	Репозитории = _СканерИспользованийПакета.НайтиРепозитории(ИмяПакета);
	Ожидаем.Что(Репозитории).Равно(Неопределено);

	УстановитьЛимит(2); // 2 страницы форков
	Репозитории = _СканерИспользованийПакета.НайтиРепозитории(ИмяПакета);
		
	// Утверждение
	Ожидаем.Что(Репозитории).ИмеетТип("Массив");
	
	Статистика = СтатистикаНайденнызРепозиториев(Репозитории);
	Ожидаем.Что(Репозитории).ИмеетДлину(200);
	Ожидаем.Что(Статистика.КоличествоОсновныхРепозиториев, "Количество основных репозиториев").Равно(150);
	Ожидаем.Что(Статистика.КоличествоФорков, "Количество форков").Равно(50);

КонецПроцедуры

Функция СтатистикаНайденнызРепозиториев(Репозитории)

	ОсновныеРепозитории = Новый МножествоСоответствие();
	Форки = Новый МножествоСоответствие();

	Для Каждого Описание Из Репозитории Цикл 
		Если Описание.ЭтоФорк Тогда
			Форки.Добавить(Описание.ПолноеИмя);
		Иначе
			ОсновныеРепозитории.Добавить(Описание.ПолноеИмя);
		КонецЕсли;
	КонецЦикла;

	Возврат Новый Структура("КоличествоОсновныхРепозиториев, КоличествоФорков", ОсновныеРепозитории.Количество(), Форки.Количество());
	
КонецФункции

Процедура ДобавитьТестовыйСлучай(ТестовыеСлучаи, КоличествоЭлементов, КоличествоОсновныхРепозиториев, КоличествоФорков)
	ТестовыйСлучай = Новый Структура();
	ТестовыйСлучай.Вставить("КоличествоЭлементов", КоличествоЭлементов);
	ТестовыйСлучай.Вставить("КоличествоОсновныхРепозиториев", КоличествоОсновныхРепозиториев);
	ТестовыйСлучай.Вставить("КоличествоФорков", КоличествоФорков);
	ТестовыеСлучаи.Добавить(ТестовыйСлучай);
КонецПроцедуры

Процедура УстановитьЛимит(Лимит)
	Результат = КоннекторHTTP.Get(_HttpBin.URL(СтрШаблон("search/limits/%1", Формат(Лимит, "ЧГ="))));
	Если Результат.КодСостояния <> 200 Тогда
		ВызватьИсключение "Не удалось установить лимит";
	КонецЕсли;
КонецПроцедуры

Процедура ОтключитьЛимит()
	Результат = КоннекторHTTP.Get(_HttpBin.URL(СтрШаблон("search/limits/disable")));
	Если Результат.КодСостояния <> 200 Тогда
		ВызватьИсключение "Не удалось отключить лимит";
	КонецЕсли;
КонецПроцедуры