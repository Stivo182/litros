#Использовать jason
#Использовать asserts

Перем _МаксимальноеКоличествоВыдачи; // Число
Перем _ДиапазонРазмераФайла; // Число
Перем _СериализаторJson; // СериализаторJson
Перем _ОстатокЛимитаЗапросов; // Число
Перем _УстановленЛимит; // Булево

&Контроллер("/search")
Процедура ПриСозданииОбъекта()
	_МаксимальноеКоличествоВыдачи = 1000;
	_ДиапазонРазмераФайла = 20000;
	_СериализаторJson = Новый СериализаторJson();
	_ОстатокЛимитаЗапросов = 0;
	_УстановленЛимит = Ложь;
КонецПроцедуры

&ТочкаМаршрута("limits/{Лимит}")
Процедура УстановкаЛимита(Ответ, Лимит) Экспорт

	Ответ.УстановитьСостояниеОК();

	Если Лимит = "disable" Тогда
		_УстановленЛимит = Ложь;
	Иначе
		_ОстатокЛимитаЗапросов = Число(Лимит);
		_УстановленЛимит = Истина;
	КонецЕсли;

КонецПроцедуры

&ТочкаМаршрута("code")
Процедура ПоискаКода(Ответ, ПараметрыЗапросаИменные) Экспорт

	НастройкиТеста = ОпределитьНастройкиТеста(ПараметрыЗапросаИменные);

	МаксимальноеКоличествоНаСтранице = ПараметрыЗапросаИменные["per_page"];
	Если ЗначениеЗаполнено(МаксимальноеКоличествоНаСтранице) Тогда
		МаксимальноеКоличествоНаСтранице = Число(МаксимальноеКоличествоНаСтранице);
	Иначе
		МаксимальноеКоличествоНаСтранице = 10;
	КонецЕсли;

	НомерСтраницы = ПараметрыЗапросаИменные["page"];
	Если ЗначениеЗаполнено(НомерСтраницы) Тогда
		НомерСтраницы = Число(НомерСтраницы);
	Иначе
		НомерСтраницы = 1;
	КонецЕсли;

	СгенерироватьОтвет(Ответ, НастройкиТеста, МаксимальноеКоличествоНаСтранице, НомерСтраницы);

КонецПроцедуры

Процедура СгенерироватьОтвет(Ответ, НастройкиТеста, МаксимальноеКоличествоНаСтранице, НомерСтраницы)

	Ответ.УстановитьТипКонтента("json");
	Ответ.УстановитьСостояниеОК();

	Если НастройкиТеста.МинимальныйРазмерФайла <> 0 Или НастройкиТеста.МаксимальныйРазмерФайла <> 0 Тогда
		Разница = НастройкиТеста.МаксимальныйРазмерФайла - НастройкиТеста.МинимальныйРазмерФайла;
		Ожидаем.Что(Разница, "Неверный диапазон файлов").Равно(_ДиапазонРазмераФайла);
	КонецЕсли;

	КоличествоДиапазонов = 5;
	НомерДиапазонаРазмераФайла = Цел(НастройкиТеста["МаксимальныйРазмерФайла"] / _ДиапазонРазмераФайла);

	Если НомерДиапазонаРазмераФайла = 0 Тогда
		КоэффициентЭлементов = 1;
	ИначеЕсли НомерДиапазонаРазмераФайла > КоличествоДиапазонов Тогда
		КоэффициентЭлементов = 0;
	Иначе
		КоэффициентЭлементов = 1 / КоличествоДиапазонов;
	КонецЕсли;
	
	КоличествоЭлементов = Окр(НастройкиТеста["КоличествоЭлементов"] * КоэффициентЭлементов, 0);
	КоличествоОснонвыхРепозиториев = НастройкиТеста["КоличествоОснонвыхРепозиториев"];
	КоличествоФорков = НастройкиТеста["КоличествоФорков"];
	КоличествоРепозиториев = ?(НастройкиТеста.ТолькоФорки, КоличествоФорков, КоличествоОснонвыхРепозиториев);

	КоличествоСтраниц = Макс(КоличествоЭлементов / МаксимальноеКоличествоНаСтранице, 1);
	КоличествоСтраниц = ?(Цел(КоличествоСтраниц) <> КоличествоСтраниц, Цел(КоличествоСтраниц) + 1, КоличествоСтраниц);

	Если КоличествоСтраниц > НомерСтраницы Тогда
		Ответ.Заголовки.Вставить("Link", "<link rel=""next"" href=""/"" />");
	КонецЕсли;
	
	Результат = Новый Структура();
	Результат.Вставить("items", Новый Массив());
	Результат.Вставить("message", "");
	Результат.Вставить("total_count", КоличествоЭлементов);
	Результат.Вставить("incomplete_results", Ложь);

	Если _УстановленЛимит Тогда
		Если _ОстатокЛимитаЗапросов = 0 Тогда
			Результат["message"] = "API rate limit exceeded";
			Ответ.УстановитьСостояние(403);
			Ответ.Заголовки.Вставить("X-RateLimit-Remaining", "0");
			Ответ.ТелоТекст = _СериализаторJson.Сериализовать(Результат);
			Возврат;
		КонецЕсли;

		_ОстатокЛимитаЗапросов = Макс(_ОстатокЛимитаЗапросов - 1, 0);
		Ответ.Заголовки.Вставить("X-RateLimit-Remaining", Формат(_ОстатокЛимитаЗапросов, "ЧН=0; ЧГ="));
	КонецЕсли;

	Если КоличествоЭлементов > _МаксимальноеКоличествоВыдачи 
		И НомерСтраницы * МаксимальноеКоличествоНаСтранице > _МаксимальноеКоличествоВыдачи Тогда

		Результат["message"] = СтрШаблон(
			"Cannot access beyond the first %1 results",
			Формат(_МаксимальноеКоличествоВыдачи, "ЧГ="));
		Ответ.ТелоТекст = _СериализаторJson.Сериализовать(Результат);
		Ответ.УстановитьСостояние(422);
		
		Возврат;

	КонецЕсли;
	
	Если КоличествоСтраниц < НомерСтраницы Тогда
		Ответ.ТелоТекст = _СериализаторJson.Сериализовать(Результат);
		Возврат;
	КонецЕсли;

	Для Индекс = 1 По МаксимальноеКоличествоНаСтранице Цикл
		
		НомерЭлемента = МаксимальноеКоличествоНаСтранице * (НомерСтраницы - 1) + Индекс;

		Если НомерЭлемента > КоличествоЭлементов Тогда
			Прервать;
		КонецЕсли;

		ЭтоНерелевантнаяВыдача = КоличествоРепозиториев = 0;

		Если Не ЭтоНерелевантнаяВыдача Тогда
			НомерРепозитория = (НомерЭлемента - 1) % КоличествоРепозиториев + 1;
			ИмяРепозитория = ?(НастройкиТеста.ТолькоФорки, "fork", "repo") + НомерРепозитория;
		Иначе
			ИмяРепозитория = "fake";
		КонецЕсли;

		ПолноеИмя = "user/" + ИмяРепозитория;

		Элемент = Новый Структура();
		Элемент.Вставить("text_matches", Новый Массив());

		Если Не ЭтоНерелевантнаяВыдача Тогда
			Элемент["text_matches"].Добавить(Новый Структура("fragment", "#Использовать " + НастройкиТеста.ИмяПакета));
		Иначе
			Элемент["text_matches"].Добавить(Новый Структура("fragment", "#Использовать " + НастройкиТеста.ИмяПакета + "fake"));
		КонецЕсли;

		Элемент.Вставить("repository", Новый Соответствие());
		Элемент["repository"]["html_url"] = "https://github.com/" + ПолноеИмя;
		Элемент["repository"]["private"] = Ложь;
		Элемент["repository"]["full_name"] = ПолноеИмя;
		Элемент["repository"]["owner"] = Новый Структура("login", "user");
		Элемент["repository"]["fork"] = НастройкиТеста.ТолькоФорки;

		Результат["items"].Добавить(Элемент);
		
	КонецЦикла;

	Ответ.ТелоТекст = _СериализаторJson.Сериализовать(Результат);

КонецПроцедуры

Функция ОпределитьНастройкиТеста(ПараметрыЗапросаИменные)

	НастройкиТеста = Новый Структура();
	НастройкиТеста.Вставить("ИмяПакета", "");
	НастройкиТеста.Вставить("ТолькоФорки", Ложь);
	НастройкиТеста.Вставить("КоличествоЭлементов", 0);
	НастройкиТеста.Вставить("КоличествоОснонвыхРепозиториев", 0);
	НастройкиТеста.Вставить("КоличествоФорков", 0);
	НастройкиТеста.Вставить("МинимальныйРазмерФайла", 0);
	НастройкиТеста.Вставить("МаксимальныйРазмерФайла", 0);

	РегулярноеВыражение = Новый РегулярноеВыражение("""#Использовать\s+([a-zA-Z_][a-zA-Z0-9_]*)""(?:.*?\b(fork:only)\b)?(?:.*?\b(size:\d+\.\.\d+)\b)?");
	Совпадения = РегулярноеВыражение.НайтиСовпадения(ПараметрыЗапросаИменные["q"]);

	Если Совпадения.Количество() = 0 Тогда
		Возврат НастройкиТеста;
	КонецЕсли;

	Совпадение = Совпадения[0];

	// Имя пакета
	ИмяПакета = Совпадение.Группы[1].Значение;
	НастройкиТеста.ИмяПакета = ИмяПакета;
	Части = СтрРазделить(ИмяПакета, "_");

	Параметры = Новый Соответствие();
	Для к = 1 По Части.Количество() / 2 Цикл
		Параметры[Части[к * 2 - 2]] = Части[к * 2 - 1];
	КонецЦикла;

	НастройкиТеста.КоличествоЭлементов = Число(Параметры["totalcount"]);
	НастройкиТеста.КоличествоОснонвыхРепозиториев = Число(Параметры["repocount"]);
	НастройкиТеста.КоличествоФорков = Число(Параметры["forkcount"]);

	// Только форки
	ТолькоФоркиТекст = Совпадение.Группы[2].Значение;
	Если ЗначениеЗаполнено(ТолькоФоркиТекст) Тогда
		НастройкиТеста.ТолькоФорки = Истина;
	КонецЕсли;

	// Размер файла
	РазмерТекст = Совпадение.Группы[3].Значение;
	Если ЗначениеЗаполнено(РазмерТекст) Тогда
		ПозицияРазделения = СтрНайти(РазмерТекст, "..") ;
		РазмерФайла = Число(Сред(РазмерТекст, 6, ПозицияРазделения - 6));
		НастройкиТеста.МинимальныйРазмерФайла = РазмерФайла;
		РазмерФайла = Число(Сред(РазмерТекст, ПозицияРазделения + 2));
		НастройкиТеста.МаксимальныйРазмерФайла = РазмерФайла;
	КонецЕсли;

	Возврат НастройкиТеста;
	
КонецФункции