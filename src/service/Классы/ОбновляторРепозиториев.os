#Использовать entity
#Использовать collectionos
#Использовать autumn-logos
#Использовать "../../dto"
#Использовать "../../repository"

&Пластилин("КлиентЛимитированныхЗапросовHTTP")
Перем _КлиентЛимитированныхЗапросовHTTP; // КлиентЛимитированныхЗапросовHTTP

&Пластилин("ХранилищеРепозиториев")
Перем _ХранилищеРепозиториев; // ХранилищеРепозиториев

&Лог("litros.ОбновляторРепозиториев")
Перем _Лог; // Лог

&Деталька("litros.ТокенGitHub") 
Перем _Токен; // Строка

&Деталька(Значение = "litros.ИнтервалПроверкиАктуальности", ЗначениеПоУмолчанию = 300) 
Перем _ИнтервалПроверкиАктуальности; // Число

&Деталька(Значение = "litros.СрокАктуальностиРепозитория", ЗначениеПоУмолчанию = 604800) 
Перем _СрокАктуальностиРепозитория; // Число

&Желудь
&Прозвище("Сервис")
Процедура ПриСозданииОбъекта()
КонецПроцедуры

#Область ПрограммныйИнтерфейс

Процедура Запустить() Экспорт

	РазмерВыборки = 50;
	МиллисекундВСекунде = 1000;
	
	_Лог.Отладка("Запуск интервального обновления репозиториев");
	
	Пока Истина Цикл

		Попытка
			ОбновитьУстаревшиеРепозитории(РазмерВыборки);
		Исключение
			ТекстОшибки = СтрШаблон("Не удалось обновить репозитории: 
			|%1", ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			_Лог.Ошибка(ТекстОшибки);
		КонецПопытки;

		Приостановить(_ИнтервалПроверкиАктуальности * МиллисекундВСекунде);

	КонецЦикла;

КонецПроцедуры

Процедура ОбновитьРепозитории(Идентификаторы) Экспорт

	Если Идентификаторы.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;

	_Лог.Отладка("Обновление репозиториев: %1", СтрРазделить(Идентификаторы, ", "));

	Ответ = ПолучитьОписанияРепозиториевИзGitHub(Идентификаторы);

	Если Ответ = Неопределено Тогда
		Возврат;
	КонецЕсли;

	ДанныеОтвета = Ответ.Json();

	ОбработатьОшибкиЗапросаКGitHub(Ответ, ДанныеОтвета);

	Если Не ЗначениеЗаполнено(ДанныеОтвета["data"]) Тогда
		Возврат;
	КонецЕсли;

	ОбработанныеРепозитории = Новый МножествоСоответствие();

	Для Каждого ДанныеРепозитория Из ДанныеОтвета["data"]["nodes"] Цикл

		ПолноеИмя = СтрШаблон("%1/%2", ДанныеРепозитория["owner"]["login"], ДанныеРепозитория["name"]);

		СущностьРепозиторий = Новый СущностьРепозиторий();
		СущностьРепозиторий.Идентификатор = ДанныеРепозитория["id"];
		СущностьРепозиторий.Имя = ДанныеРепозитория["name"];
		СущностьРепозиторий.ПолноеИмя = ПолноеИмя;
		СущностьРепозиторий.URL = ДанныеРепозитория["url"];
		СущностьРепозиторий.Владелец = ДанныеРепозитория["owner"]["login"];
		СущностьРепозиторий.Описание = ДанныеРепозитория["description"];
		СущностьРепозиторий.ЭтоФорк = ДанныеРепозитория["isFork"];
		СущностьРепозиторий.КоличествоЗвезд = ДанныеРепозитория["stargazerCount"];
		СущностьРепозиторий.КоличествоФорков = ДанныеРепозитория["forkCount"];

		_ХранилищеРепозиториев.Сохранить(СущностьРепозиторий);
		ОбработанныеРепозитории.Добавить(ДанныеРепозитория["id"]);

	КонецЦикла;

	Для Каждого Идентификатор Из Идентификаторы Цикл
		Если Не ОбработанныеРепозитории.Содержит(Идентификатор) Тогда
			_ХранилищеРепозиториев.Удалить(Идентификатор);
		КонецЕсли;
	КонецЦикла;

КонецПроцедуры

Процедура ОбновитьУстаревшиеРепозитории(РазмерВыборки) Экспорт
	
	ДатаОкончания = ТекущаяУниверсальнаяДата() - _СрокАктуальностиРепозитория;

	_Лог.Отладка("Запуск обновления репозиториев");
	
	Идентификаторы = _ХранилищеРепозиториев.ПолучитьРепозиторииДляОбновления(ДатаОкончания, РазмерВыборки);
	ОбновитьРепозитории(Идентификаторы);

	_Лог.Отладка("Завершение обновления репозиториев");
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ПолучитьОписанияРепозиториевИзGitHub(Идентификаторы)
	
	Заголовки = Новый Соответствие();

	Если ЗначениеЗаполнено(_Токен) Тогда
		Заголовки.Вставить("Authorization", "Bearer " + _Токен);
	КонецЕсли;

	ДополнительныеПараметры = Новый Структура();
	ДополнительныеПараметры.Вставить("Заголовки", Заголовки);

	Попытка
		Ответ = _КлиентЛимитированныхЗапросовHTTP.Post("https://api.github.com/graphql", , 
			ПодготовитьЗапросGraphQL(Идентификаторы),
			ДополнительныеПараметры);
	Исключение
		_Лог.Ошибка(КраткоеПредставлениеОшибки(ИнформацияОбОшибке()));
		Возврат Неопределено;
	КонецПопытки;
	
	Возврат Ответ;

КонецФункции

Функция ПодготовитьЗапросGraphQL(Идентификаторы)

	ТекстЗапроса = "query GetRepos($ids: [ID!]!){ 
	|	nodes(ids: $ids) {
	|		... on Repository {
	|			id
	|			name
	|			description
	|			url
	|			isFork
	|			stargazerCount
	|			forkCount
	|			owner {
	|				login
	|			}
	|		}
	|	}
	|}";
	
	Переменные = Новый Структура("ids", Новый Массив());
	Для Каждого Идентификатор Из Идентификаторы Цикл
		Переменные["ids"].Добавить(Идентификатор);
	КонецЦикла;

	Возврат Новый Структура("query, variables", ТекстЗапроса, Переменные);

КонецФункции

Процедура ОбработатьОшибкиЗапросаКGitHub(Ответ, ДанныеОтвета)

	Если Ответ.КодСостояния <> 200 Тогда
		ТекстОшибки = Ответ.Текст();
	Иначе
		Ошибки = Новый Массив();

		Если ДанныеОтвета["errors"] <> Неопределено Тогда
			Для Каждого Ошибка Из ДанныеОтвета["errors"] Цикл
				Ошибки.Добавить(Ошибка["message"]);
			КонецЦикла;
		КонецЕсли;

		ТекстОшибки = СтрСоединить(Ошибки, Символы.ПС);
	КонецЕсли;

	Если ЗначениеЗаполнено(ТекстОшибки) Тогда
		ТекстОшибки = СтрШаблон(
			"Не удалось получить информацию об репозиториях при выполнении запроса к Github: %1",
			ТекстОшибки);
		_Лог.Ошибка(ТекстОшибки);
	КонецЕсли;

КонецПроцедуры

#КонецОбласти