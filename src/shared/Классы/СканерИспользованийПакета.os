// BSLLS:LatinAndCyrillicSymbolInWord-off

#Использовать collectionos
#Использовать logos
#Использовать asserts
#Использовать "../../internal"

Перем _КлиентЛимитированныхЗапросовHTTP; // КлиентЛимитированныхЗапросовHTTP
Перем _НакопленныеИспользованияПакета;   // НакопленныеИспользованияПакета
Перем _Лог;                              // Лог
Перем _Токен;                            // Строка
Перем _АдресAPI;                         // Строка
Перем _ПоискПодключенныхПакетов;         // РегулярноеВыражение

// Конструктор сканера зависимых репозиториев GitHub.
//
// Параметры:
//   Токен     - Строка   - Персональный токен доступа GitHub (Personal Access Token).
//   АдресAPI  - Строка   - Базовый адрес GitHub API.
//                          Если не задан, используется https://api.github.com.
//   КлиентHTTP - Произвольный - HTTP-клиент для выполнения запросов к API.
//                               Если не задан, создается автоматически.
&Желудь
Процедура ПриСозданииОбъекта(
	&Деталька("litros.ТокенGitHub") Токен,
	&Деталька("litros.АдресGitHubAPI") АдресAPI = "",
	&Пластилин("КлиентЛимитированныхЗапросовHTTP") КлиентHTTP = Неопределено
)

	_Токен = Токен;
	_АдресAPI = АдресAPI;
	_КлиентЛимитированныхЗапросовHTTP = КлиентHTTP;
	_Лог = Логирование.ПолучитьЛог("litros.ПоисковикЗависимыхРепозиториев");
	_НакопленныеИспользованияПакета = Новый НакопленныеИспользованияПакета();

	Если Не ЗначениеЗаполнено(_АдресAPI) Тогда
		_АдресAPI = "https://api.github.com";
	КонецЕсли;

	Если _КлиентЛимитированныхЗапросовHTTP = Неопределено Тогда
		_КлиентЛимитированныхЗапросовHTTP = Новый КлиентЛимитированныхЗапросовHTTP();
	КонецЕсли;

	_ПоискПодключенныхПакетов = Новый РегулярноеВыражение("^\s*#(Использовать|Use)\s+([a-zA-Z0-9\-_]+).*");
	_ПоискПодключенныхПакетов.Многострочный = Истина;
	_ПоискПодключенныхПакетов.ИгнорироватьРегистр = Истина;

КонецПроцедуры

#Область ПрограммныйИнтерфейс

// Выполняет поиск репозиториев GitHub, в исходном коде которых
// обнаружено подключение указанного пакета OneScript.
//
// Параметры:
//   ИмяПакета - Строка - Имя анализируемого пакета
//
// Возвращаемое значение:
//   Структура - Результат выполнения поиска со следующими полями:
//     * Данные - Массив из Структура - Список уникальных репозиториев, в которых найдено использование пакета:
//       ** Идентификатор - Строка - Идентификатор
//       ** URL           - Строка - URL репозитория
//       ** ПолноеИмя     - Строка - Полное имя репозитория (owner/name)
//       ** Владелец      - Строка - Логин владельца
//       ** ЭтоФорк       - Булево - Признак форка
//     * Успех     - Булево - Признак отсутствия ошибок при выполнении запроса к GitHub API.
//     * Завершено - Булево - Признак полного завершения поиска.
//                            Ложь означает, что поиск временно приостановлен и будет продолжен
//                            при следующем вызове метода.
//     * Сообщение - Строка - Диагностическое сообщение.
Функция НайтиРепозитории(Знач ИмяПакета) Экспорт

	ИмяПакета = РаботаСДаннымиБД.НормализироватьИмяПакета(ИмяПакета);
	
	Ожидаем.Что(ИмяПакета, "Не заполнено имя пакета").Заполнено();

	Результат = Новый Структура();
	Результат.Вставить("Данные", Новый Массив());
	Результат.Вставить("Успех", Ложь);
	Результат.Вставить("Завершено", Ложь);
	Результат.Вставить("Сообщение", "");

	НайденныеРепозитории = Новый КартаСоответствие();
	_НакопленныеИспользованияПакета.УдалитьУстаревшиеДанные();
	
	НакопленныеДанные = _НакопленныеИспользованияПакета.Получить(ИмяПакета);
	Если НакопленныеДанные = Неопределено Тогда
		НомерСтраницы = 1;
		ШагДиапазонаРазмераФайла = 0;
		ТолькоФорки = Ложь;
	Иначе
		НомерСтраницы = НакопленныеДанные["НомерСтраницы"];
		ШагДиапазонаРазмераФайла = НакопленныеДанные["ШагДиапазонаРазмераФайла"];
		ТолькоФорки = НакопленныеДанные["ТолькоФорки"];
		НайденныеРепозитории = НакопленныеДанные["Репозитории"];	
	КонецЕсли;

	_Лог.Отладка(
		"Начат поиск репозиториев, зависящих от пакета %1
		|Номер страницы: %2, Шаг диапазона размера файла: %3, Только форки: %4, Найдено всего репозиториев: %5",
		ИмяПакета,
		НомерСтраницы,
		ШагДиапазонаРазмераФайла,
		ТолькоФорки,
		НайденныеРепозитории.Количество()
	);

	РезультатПоиска = НайтиРепозиторииСоСтраницы(ИмяПакета,
		НомерСтраницы,
		ШагДиапазонаРазмераФайла,
		ТолькоФорки,
		НайденныеРепозитории);

	Если РезультатПоиска.Успех Тогда

		_НакопленныеИспользованияПакета.Удалить(ИмяПакета);

		_Лог.Отладка(
			"Успешно завершён поиск репозиториев, зависящих от пакета %1. Обнаружено репозиториев: %2",
			ИмяПакета,
			НайденныеРепозитории.Количество()
		);

		Результат.Успех = Истина;
		Результат.Завершено = Истина;
		Результат.Данные = НайденныеРепозитории.Значения().ВМассив();

		Возврат Результат;

	КонецЕсли;

	НужноПоместитьВоВременноеХранилище = Не РезультатПоиска.Успех
		И (НомерСтраницы > 1 
			Или ШагДиапазонаРазмераФайла > 1 
			Или ТолькоФорки); // Форки запрашиваются последними
	
	Если НужноПоместитьВоВременноеХранилище Тогда

		НакопленныеДанные = _НакопленныеИспользованияПакета.Добавить(ИмяПакета);	
		НакопленныеДанные.НомерСтраницы = НомерСтраницы;
		НакопленныеДанные.ШагДиапазонаРазмераФайла = ШагДиапазонаРазмераФайла;
		НакопленныеДанные.ТолькоФорки = ТолькоФорки;
		НакопленныеДанные.Репозитории = НайденныеРепозитории;

		_Лог.Отладка(
			"Найденные репозитории, зависящие от пакета %1, помещены во временное хранилище для последующей обработки.
			|Номер страницы: %2, Шаг диапазона размера файла: %3, Только форки: %4, Найдено всего репозиториев: %5",
			ИмяПакета,
			НомерСтраницы,
			ШагДиапазонаРазмераФайла,
			ТолькоФорки,
			НайденныеРепозитории.Количество()
		);

		Результат.Успех = Истина;

	КонецЕсли;

	Возврат Результат;

КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция НайтиРепозиторииСоСтраницы(ИмяПакета, НомерСтраницы, ШагДиапазонаРазмераФайла, ТолькоФорки, НайденныеРепозитории)

	Результат = Новый Структура();
	Результат.Вставить("Успех", Истина);
	Результат.Вставить("Сообщение", "");

	Пока Истина Цикл

		РезультатПоиска = НайтиРепозиторииНаСтранице(ИмяПакета,
			НомерСтраницы,
			ШагДиапазонаРазмераФайла,
			ТолькоФорки,
			НайденныеРепозитории);
			
		Результат.Успех = РезультатПоиска.Успех;
		Результат.Сообщение = РезультатПоиска.Сообщение;

		ПовыситьШагДиапазонаРазмераФайла = Результат.Успех 
			И РезультатПоиска.ЕстьДанные
			И Не РезультатПоиска.ЕстьСледующаяСтраница
			И ШагДиапазонаРазмераФайла > 0;

		ПерейтиНаФильтрациюПоРазмеруФайла = РезультатПоиска.ДостигнутМаксимумРезультатов И ШагДиапазонаРазмераФайла = 0;

		ПереключитьсяНаФорки = Не ТолькоФорки
			И Результат.Успех
			И Не РезультатПоиска.ЕстьСледующаяСтраница
			И Не ПовыситьШагДиапазонаРазмераФайла
			И Не ПерейтиНаФильтрациюПоРазмеруФайла;

		Если Результат.Успех И РезультатПоиска.ЕстьСледующаяСтраница Тогда

			НомерСтраницы = НомерСтраницы + 1;

			_Лог.Отладка(
				"Переход на страницу %1 при поиске репозиториев, зависящих от пакета %2",
				НомерСтраницы,
				ИмяПакета
			);

		ИначеЕсли ПовыситьШагДиапазонаРазмераФайла Или ПерейтиНаФильтрациюПоРазмеруФайла Тогда

			НомерСтраницы = 1;
			ШагДиапазонаРазмераФайла = ШагДиапазонаРазмераФайла + 1;

			_Лог.Отладка(
				"Увеличен шаг диапазона размера файла до %1 при поиске репозиториев, зависящих от пакета %2",
				ШагДиапазонаРазмераФайла,
				ИмяПакета
			);

		ИначеЕсли ПереключитьсяНаФорки Тогда

			ТолькоФорки = Истина;
			НомерСтраницы = 1;
			ШагДиапазонаРазмераФайла = 0;

		Иначе
			Прервать;
		КонецЕсли;

	КонецЦикла;

	Возврат Результат;

КонецФункции

Функция НайтиРепозиторииНаСтранице(ИмяПакета, НомерСтраницы, ШагДиапазонаРазмераФайла, ТолькоФорки, НайденныеРепозитории)
			
	МаксимальноеКоличествоПоисковойВыдачи = 1000;

	Результат = Новый Структура();
	Результат.Вставить("Успех", Ложь);
	Результат.Вставить("Сообщение", "");
	Результат.Вставить("ЕстьДанные", Ложь);
	Результат.Вставить("ЕстьСледующаяСтраница", Ложь);
	Результат.Вставить("ДостигнутМаксимумРезультатов", Ложь);

	РезультатПоиска = НайтиИспользованияПакетаВКодеРепозиториевGitHub(ИмяПакета,
		НомерСтраницы,
		ШагДиапазонаРазмераФайла,
		ТолькоФорки);

	Если Не РезультатПоиска.Успех Тогда
		Результат.Сообщение = РезультатПоиска.Сообщение;
		Возврат Результат;
	КонецЕсли;

	Ответ = РезультатПоиска.Ответ;
	ДанныеОтвета = Ответ.Json();
	
	Если Ответ.КодСостояния <> 200 Тогда

		Если ДанныеОтвета["message"] = "Cannot access beyond the first 1000 results" Тогда
			Результат.ДостигнутМаксимумРезультатов = Истина;
		КонецЕсли;

		Результат.Сообщение = СтрШаблон("Не удалось получить информацию об использовании пакета '%1': %2",
			ИмяПакета,
			ДанныеОтвета["message"]);

		_Лог.Ошибка(Результат.Сообщение);

		Возврат Результат;

	КонецЕсли;

	КоличествоИспользований = ДанныеОтвета["total_count"];
	ЭтоНеполныйРезультат = ДанныеОтвета["incomplete_results"];
	НайденныеЭлементы = ДанныеОтвета["items"];

	Результат.ЕстьДанные = КоличествоИспользований > 0;
	Результат.ЕстьСледующаяСтраница = СтрНайти(Ответ.Заголовки["Link"], "rel=""next""") > 0;
	
	Если ЭтоНеполныйРезультат Тогда
		Результат.Сообщение = "Запрос к GitHub вернул неполные данные. Вероятно, достигнут лимит запросов.";
		_Лог.Ошибка(Результат.Сообщение);
		Возврат Результат;
	КонецЕсли;
		
	Если КоличествоИспользований = 0 Тогда
		Результат.Успех = Истина;
		Возврат Результат;
	КонецЕсли;

	Если ШагДиапазонаРазмераФайла = 0 И КоличествоИспользований > МаксимальноеКоличествоПоисковойВыдачи Тогда
		Результат.ДостигнутМаксимумРезультатов = Истина;
		Возврат Результат;
	КонецЕсли;

	Для Каждого Элемент Из НайденныеЭлементы Цикл

		АдресРепозитория = Элемент["repository"]["html_url"];
		ЭтоПриватныйРепозиторий = Элемент["repository"]["private"];
		Совпадения = Элемент["text_matches"];

		Если ЭтоПриватныйРепозиторий Тогда
			Продолжить;
		КонецЕсли;

		НайденПакет = Ложь;
		Для Каждого Совпадение Из Совпадения Цикл
			Фрагмент = Совпадение["fragment"];
			Если ФрагментСодержитПодключениеПакета(Фрагмент, ИмяПакета) Тогда
				НайденПакет = Истина;
				Прервать;
			КонецЕсли;
		КонецЦикла;

		Если НайденПакет И Не НайденныеРепозитории.СодержитКлюч(АдресРепозитория) Тогда
			ОписаниеРепозитория = Новый Структура();
			ОписаниеРепозитория.Вставить("Идентификатор", Элемент["repository"]["node_id"]);
			ОписаниеРепозитория.Вставить("URL", АдресРепозитория);
			ОписаниеРепозитория.Вставить("ПолноеИмя", Элемент["repository"]["full_name"]);
			ОписаниеРепозитория.Вставить("Владелец", Элемент["repository"]["owner"]["login"]);
			ОписаниеРепозитория.Вставить("ЭтоФорк", Элемент["repository"]["fork"] = Истина);

			НайденныеРепозитории.Вставить(АдресРепозитория, ОписаниеРепозитория);
		КонецЕсли;

	КонецЦикла;
	
	Результат.Успех = Истина;

	Возврат Результат;

КонецФункции

Функция НайтиИспользованияПакетаВКодеРепозиториевGitHub(ИмяПакета, НомерСтраницы, ШагДиапазонаРазмераФайла, ТолькоФорки)

	МаксимальноеКоличествоНаСтранице = 100; // max 100
	ДиапазонРазмераФайла = 20000; // байт
	ФорматнаяСтрокаЧисла = "ЧН=0; ЧГ=";

	ШаблонПоиска = """%1"" extension:os %2 %3";
	ФильтрПоРазмеру = "";
	ФильтрПоФоркам = "";

	Результат = Новый Структура();
	Результат.Вставить("Успех", Ложь);
	Результат.Вставить("Ответ", Неопределено);
	Результат.Вставить("Сообщение", "");

	// Параметр fork:true в эндпоинте https://api.github.com/search/code предназначен 
	// для включения в результаты поиска кода из форков репозиториев в дополнение к оригинальным. 
	// Однако на практике поведение этого параметра нестабильно: в некоторых запросах оригинальные 
	// репозитории полностью исчезают из выдачи, и возвращаются только результаты из форков.
	Если ТолькоФорки Тогда
		ФильтрПоФоркам = "fork:only";
	КонецЕсли;

	Если ШагДиапазонаРазмераФайла > 0 Тогда
		ФильтрПоРазмеру = СтрШаблон("size:%1..%2", 
			Формат((ШагДиапазонаРазмераФайла - 1) * ДиапазонРазмераФайла, ФорматнаяСтрокаЧисла)
			Формат(ШагДиапазонаРазмераФайла * ДиапазонРазмераФайла, ФорматнаяСтрокаЧисла));
	КонецЕсли;

	СтрокаПоиска = СтрШаблон(ШаблонПоиска,
		ТекстПоискаПоИспользованиюПакета(ИмяПакета),
		ФильтрПоФоркам,
		ФильтрПоРазмеру);

	Заголовки = Новый Соответствие();
	Заголовки.Вставить("Accept", "application/vnd.github.text-match+json");
	Заголовки.Вставить("X-GitHub-Api-Version", "2022-11-28");

	Если ЗначениеЗаполнено(_Токен) Тогда
		Заголовки.Вставить("Authorization", "Bearer " + _Токен);
	КонецЕсли;

	ДополнительныеПараметры = Новый Структура();
	ДополнительныеПараметры.Вставить("Заголовки", Заголовки);

	ПараметрыЗапроса = Новый Структура();
	ПараметрыЗапроса.Вставить("q", СтрокаПоиска);
	ПараметрыЗапроса.Вставить("per_page", Формат(МаксимальноеКоличествоНаСтранице, "ЧГ="));
	ПараметрыЗапроса.Вставить("page", Формат(НомерСтраницы, "ЧГ="));

	Попытка
		URL = _АдресAPI + ?(СтрЗаканчиваетсяНа(_АдресAPI, "/"), "", "/")+ "search/code";
		Результат.Ответ = _КлиентЛимитированныхЗапросовHTTP.Get(URL,
			ПараметрыЗапроса,
			ДополнительныеПараметры);
		Результат.Успех = Результат.Ответ <> Неопределено;
	Исключение
		Результат.Сообщение = КраткоеПредставлениеОшибки(ИнформацияОбОшибке());
		_Лог.Ошибка(Результат.Сообщение);
	КонецПопытки;

	Возврат Результат;

КонецФункции

Функция ТекстПоискаПоИспользованиюПакета(ИмяПакета)
	Возврат СтрШаблон("#Использовать %1", ИмяПакета);
КонецФункции

Функция ФрагментСодержитПодключениеПакета(Фрагмент, ИмяПакета)

	Совпадения = _ПоискПодключенныхПакетов.НайтиСовпадения(Фрагмент);
	Для Каждого Совпадение Из Совпадения Цикл	
		НайденноеИмяПакета = НРег(Совпадение.Группы[2].Значение);
		Если НайденноеИмяПакета = НРег(ИмяПакета) Тогда
			Возврат Истина;
		КонецЕсли;
	КонецЦикла;

	Возврат Ложь;

КонецФункции

#КонецОбласти