#Использовать 1connector
#Использовать entity
#Использовать jason
#Использовать "../dto"

&Пластилин("ПроцессорОчередиПакетов")
Перем _ПроцессорОчередиПакетов; // ПроцессорОчередиПакетов

&Пластилин("ХранилищеЗависимыхРепозиториев")
Перем _ХранилищеЗависимыхРепозиториев; // ХранилищеЗависимыхРепозиториев

&Пластилин("ФабрикаОтветов")
Перем _ФабрикаОтветов; // ФабрикаОтветов (см. winow)

&Пластилин("МенеджерОтображений")
Перем _МенеджерОтображений; // МенеджерОтображений (см. winow)

&Пластилин("ВалидаторИмениПакета")
Перем _ВалидаторИмениПакета; // ВалидаторИмениПакета

&Лог("litros.webapp")
Перем _Лог; // Лог

&Контроллер("/")
Процедура ПриСозданииОбъекта()

КонецПроцедуры

#Область ТочкиМаршрута

&ТочкаМаршрута("/")
&Отображение("./src/webapp/view/index.html")
Процедура Главная(Ответ) Экспорт

КонецПроцедуры

&ТочкаМаршрута("{Значение}")
Процедура ТочкаПакета(Ответ, Значение) Экспорт

	Страница = РазобратьИмяСтраницы(Значение);
	ИмяПакета = Страница.ИмяПакета;
	Расширение = Страница.Расширение;
	ТекстОшибки = "";

	ПроверитьПакет(ИмяПакета, ТекстОшибки);
	
	Если Не ЗначениеЗаполнено(Расширение) 
		И Не ЗначениеЗаполнено(ТекстОшибки) Тогда
		Ответ.Перенаправить("/?package=" + ИмяПакета);
	ИначеЕсли Расширение = "svg" Тогда
		ОтветБейдж(ИмяПакета, ТекстОшибки, Ответ);
	ИначеЕсли Расширение = "json" Тогда
		ОтветJson(ИмяПакета, ТекстОшибки, Ответ);
	Иначе
		Ответ404(ТекстОшибки, Ответ);
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ПроверитьПакет(ИмяПакета, ТекстОшибки)
		
	ТекстОшибки = _ВалидаторИмениПакета.Валидировать(ИмяПакета);

	Если ЗначениеЗаполнено(ТекстОшибки) Тогда
		Возврат;
	КонецЕсли;

	Если Не _ХранилищеЗависимыхРепозиториев.ЕстьДанныеПоПакету(ИмяПакета) Тогда
		ТекстОшибки = "Данные по пакету пока отсутствуют. Сбор информации поставлен в очередь - результаты будут доступны позже.";
		_ПроцессорОчередиПакетов.ДобавитьВОчередь(ИмяПакета);
	КонецЕсли;

КонецПроцедуры

Функция РазобратьИмяСтраницы(Значение)

	Части = СтрРазделить(Значение, ".", Ложь);
	Если Части.Количество() = 2 Тогда
		ИмяПакета = Части[0];
		Расширение = НРег(Части[1]);
	Иначе
		ИмяПакета = Значение;
		Расширение = "";
	КонецЕсли;

	Возврат Новый Структура("ИмяПакета, Расширение", ИмяПакета, Расширение);

КонецФункции

Процедура ОтветБейдж(ИмяПакета, ТекстОшибки, Ответ)

	КоличествоЗависимыхРепозиториев = 0;

	Если Не ЗначениеЗаполнено(ТекстОшибки) Тогда
		КоличествоЗависимыхРепозиториев = _ХранилищеЗависимыхРепозиториев.КоличествоРепозиториев(ИмяПакета);
	КонецЕсли;
	
	Ответ.Заголовки["Content-Type"] = "image/svg+xml";
	Ответ.ТелоДвоичныеДанные = ПолучитьДвоичныеДанныеБейджа(КоличествоЗависимыхРепозиториев, ТекстОшибки);

КонецПроцедуры

Процедура ОтветJson(ИмяПакета, ТекстОшибки, Ответ)

	Результат = Новый Структура();
	Результат.Вставить("package_name", ИмяПакета);
	Результат.Вставить("repositories", Новый Массив());

	Ответ.УстановитьТипКонтента("json");		
	
	Если ЗначениеЗаполнено(ТекстОшибки) Тогда
		Результат.Вставить("message", ТекстОшибки);	
	Иначе
		Результат["repositories"] = _ХранилищеЗависимыхРепозиториев.ПолучитьСписокРепозиториев(ИмяПакета);
	КонецЕсли;

	Сериализатор = Новый СериализаторJson();
	
	Ответ.ТелоТекст = Сериализатор.Сериализовать(Результат);

КонецПроцедуры

Процедура Ответ404(ТекстОшибки, Ответ)

	ТекстСообщения = ?(ЗначениеЗаполнено(ТекстОшибки), ТекстОшибки, "Страница не найдена");

	Ответ.УстановитьСостояниеНеНайдено();
	Ответ.УстановитьТекстШаблона(_МенеджерОтображений.ПолучитьОтображение(404));

	Ответ.Модель = Новый Структура();
	Ответ.Модель.Вставить("КодСостояния", 404);
	Ответ.Модель.Вставить("ТекстСообщения", ТекстСообщения);

КонецПроцедуры

Функция ПолучитьДвоичныеДанныеБейджа(КоличествоЗависимыхРепозиториев, ТекстОшибки)

	ШаблонКоличество = "Used by-%1 repos-#65ADF1";
	ШаблонОжидает = "Used by-pending...-red";
	ШаблонОшибка = "Used by-%1-red";
	
	Если ЗначениеЗаполнено(ТекстОшибки) Тогда
		Значение = СтрШаблон(ШаблонОшибка, ТекстОшибки);
	ИначеЕсли КоличествоЗависимыхРепозиториев = Неопределено Тогда
		Значение = ШаблонОжидает;
	Иначе
		Значение = СтрШаблон(ШаблонКоличество, Формат(КоличествоЗависимыхРепозиториев, "ЧН=0; ЧГ="));
	КонецЕсли;

	Значение = КодироватьСтроку(Значение, СпособКодированияСтроки.КодировкаURL);

	URL = "https://img.shields.io/badge/" + Значение + "?logo=github";
	Возврат КоннекторHTTP.Get(URL).ДвоичныеДанные();

КонецФункции

#КонецОбласти