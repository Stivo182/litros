#Использовать 1connector
#Использовать entity
#Использовать "../dto"

&Пластилин("ПроцессорОчередиПакетов")
Перем _ПроцессорОчередиПакетов; // ПроцессорОчередиПакетов

&Пластилин("МенеджерСущностей")
Перем _МенеджерСущностей; // МенеджерСущностей (см. entity)

&Пластилин("ВалидаторИмениПакета")
Перем _ВалидаторИмениПакета; // ВалидаторИмениПакета

&Лог("litros.webapp")
Перем _Лог; // Лог

&Контроллер("/")
Процедура ПриСозданииОбъекта()

КонецПроцедуры

&ТочкаМаршрута("badge/{ИмяПакета}")
Процедура Бейдж(Ответ, ИмяПакета) Экспорт

	Количество = 0;
	ТекстОшибки = _ВалидаторИмениПакета.Валидировать(ИмяПакета);
	
	Если Не ЗначениеЗаполнено(ТекстОшибки) Тогда
		Количество = КоличествоОбратныхЗависимостей(ИмяПакета);
	КонецЕсли;
	
	Ответ.Заголовки["Content-Type"] = "image/svg+xml";
	Ответ.ТелоДвоичныеДанные = ПолучитьДвоичныеДанныеБейджа(Количество, ТекстОшибки);

КонецПроцедуры

Функция ПолучитьДвоичныеДанныеБейджа(КоличествоОбратныхЗависимостей, ТекстОшибки)

	ШаблонКоличество = "Used by-%1 repos-#65ADF1";
	ШаблонОжидает = "Used by-pending...-red";
	ШаблонОшибка = "Used by-%1-red";
	
	Если ЗначениеЗаполнено(ТекстОшибки) Тогда
		Значение = СтрШаблон(ШаблонОшибка, ТекстОшибки);
	ИначеЕсли КоличествоОбратныхЗависимостей = Неопределено Тогда
		Значение = ШаблонОжидает;
	Иначе
		Значение = СтрШаблон(ШаблонКоличество, Формат(КоличествоОбратныхЗависимостей, "ЧН=0; ЧГ="));
	КонецЕсли;

	Значение = КодироватьСтроку(Значение, СпособКодированияСтроки.КодировкаURL);

	URL = "https://img.shields.io/badge/" + Значение + "?logo=github";
	Возврат КоннекторHTTP.Get(URL).ДвоичныеДанные();

КонецФункции

Функция КоличествоОбратныхЗависимостей(ИмяПакета)
		
	ОпцииПоиска = Новый ОпцииПоиска();
	ОпцииПоиска.Отбор("ИмяПакета", ВидСравнения.Равно, ИмяПакета);

	Попытка
		СтатистикаПакета = _МенеджерСущностей.ПолучитьОдно(Тип("СущностьСтатистикаПакета"), ОпцииПоиска);
	Исключение
		ТекстОшибки = СтрШаблон("Возникла ошибка при получении статистики пакета '%1':
		|%2", ИмяПакета, ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		_Лог.Ошибка(ТекстОшибки);
		Возврат 0;
	КонецПопытки;

	Если СтатистикаПакета = Неопределено Тогда
		_ПроцессорОчередиПакетов.ДобавитьВОчередь(ИмяПакета);
		Возврат Неопределено;
	КонецЕсли;

	Возврат СтатистикаПакета.КоличествоОбратныхЗависимостей;

КонецФункции