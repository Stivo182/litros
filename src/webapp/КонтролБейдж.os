#Использовать 1connector
#Использовать entity

&Пластилин("ПроцессорОчередиПакетов")
Перем _ПроцессорОчередиПакетов; // ПроцессорОчередиПакетов

&Пластилин("МенеджерСущностей")
Перем _МенеджерСущностей; // МенеджерСущностей (см. entity)

&Лог("litros.webapp")
Перем _Лог; // Лог

&Контроллер("/")
Процедура ПриСозданииОбъекта()

КонецПроцедуры

&ТочкаМаршрута("badge/{ИмяПакета}")
Процедура Бейдж(Ответ, ИмяПакета) Экспорт

	Если ЗначениеЗаполнено(ИмяПакета) Тогда
		Количество = КоличествоОбратныхЗависимостей(ИмяПакета);
	Иначе
		Количество = 0;
	КонецЕсли;

	Ответ.Заголовки["Content-Type"] = "image/svg+xml";
	Ответ.ТелоДвоичныеДанные = ПолучитьДвоичныеДанныеБейджа(Количество);

КонецПроцедуры

Функция ПолучитьДвоичныеДанныеБейджа(КоличествоОбратныхЗависимостей)
	Количество = Формат(КоличествоОбратныхЗависимостей, "ЧН=0; ЧГ=");
	URL = "https://img.shields.io/badge/Used%20by-" + Количество + "%20repos-%2365ADF1?logo=github";
	Возврат КоннекторHTTP.Get(URL).ДвоичныеДанные();
КонецФункции

Функция КоличествоОбратныхЗависимостей(ИмяПакета)
		
	ОпцииПоиска = Новый ОпцииПоиска();
	ОпцииПоиска.Отбор("ИмяПакета", ВидСравнения.Равно, ИмяПакета);

	Попытка
		СтатистикаПакета = _МенеджерСущностей.ПолучитьОдно(Тип("СущностьСтатистикаПакета"), ОпцииПоиска);
	Исключение
		ТекстОшибки = СтрШаблон("Возникла ошибка при получении статистики пакета '%1':
		|%2", ИмяПакета, ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		_Лог.Ошибка(ТекстОшибки);
		Возврат 0;
	КонецПопытки;

	Если СтатистикаПакета = Неопределено Тогда
		_ПроцессорОчередиПакетов.ДобавитьВОчередь(ИмяПакета);
		Возврат 0;
	КонецЕсли;

	Возврат СтатистикаПакета.КоличествоОбратныхЗависимостей;

КонецФункции