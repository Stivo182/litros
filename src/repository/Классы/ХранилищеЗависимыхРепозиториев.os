#Использовать entity
#Использовать collectionos
#Использовать "../../dto"

&Табакерка
&Пластилин("МенеджерСущностей")
Перем _МенеджерСущностей; // Табакерка - МенеджерСущностей (см. entity)

&Пластилин("ХранилищеРепозиториев")
Перем _ХранилищеРепозиториев; // ХранилищеРепозиториев

&Лог("litros.ХранилищеЗависимыхРепозиториев")
Перем _Лог; // Лог

&Желудь
Процедура ПриСозданииОбъекта()

КонецПроцедуры

#Область ПрограммныйИнтерфейс

// Сохраняет репозитории, зависящих от пакета
//
// Параметры:
//   ИмяПакета - Строка - Имя пакета
//   ЗависимыеРепозитории - Массив из Структура - Список репозиториев:
//     * Идентификатор - Строка - Глобальный идентификатор GitHub
//     * URL           - Строка - URL репозитория
//     * ПолноеИмя     - Строка - Полное имя репозитория
//     * Владелец      - Строка - Логин владельца
//     * ЭтоФорк       - Булево - Признак форка
Процедура Сохранить(Знач ИмяПакета, ЗависимыеРепозитории) Экспорт

	ИмяПакета = РаботаСДаннымиБД.НормализироватьИмяПакета(ИмяПакета);
	КоличествоОбратныхЗависимостей = ЗависимыеРепозитории.Количество();

	МенеджерСущностей().НачатьТранзакцию();

	Попытка
		ОбновитьСтатистикуПакета(ИмяПакета, КоличествоОбратныхЗависимостей);
		ОбновитьЗависимыеРепозитории(ИмяПакета, ЗависимыеРепозитории);
		ДобавитьНовыеРепозитории(ЗависимыеРепозитории);
		МенеджерСущностей().ЗафиксироватьТранзакцию();
	Исключение
		МенеджерСущностей().ОтменитьТранзакцию();
		ТекстОшибки = СтрШаблон("Ошибка при сохранении зависимых репозиториев пакета %1: %2",
			ИмяПакета,
			ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		ВызватьИсключение ТекстОшибки;
	КонецПопытки;

КонецПроцедуры

// Возвращает количество репозиториев, зависящих от пакета 
//
// Параметры:
//   ИмяПакета - Строка - Имя пакета
//
// Возвращаемое значение:
//   Число, Неопределено
Функция КоличествоРепозиториев(Знач ИмяПакета) Экспорт

	ИмяПакета = РаботаСДаннымиБД.НормализироватьИмяПакета(ИмяПакета);

	ОпцииПоиска = Новый ОпцииПоиска();
	ОпцииПоиска.Отбор("ИмяПакета", ВидСравнения.Равно, ИмяПакета);

	СтатистикаПакета = МенеджерСущностей().ПолучитьОдно(Тип("СущностьСтатистикаПакета"), ОпцииПоиска);

	Если Не СтатистикаПакета = Неопределено Тогда
		Возврат СтатистикаПакета.КоличествоОбратныхЗависимостей;
	КонецЕсли;

КонецФункции

// Возвращает описания репозиториев, зависящих от пакета
//
// Параметры:
//   ИмяПакета - Строка - Имя пакета
//   Сортировка - Строка - Список имён полей с указанием направления сортировки, разделённых запятыми
//
// Возвращаемое значение:
//   Массив из СущностьРепозиторий
Функция ПолучитьРепозитории(Знач ИмяПакета, Сортировка = "") Экспорт

	ИмяПакета = РаботаСДаннымиБД.НормализироватьИмяПакета(ИмяПакета);

	ОпцииПоиска = Новый ОпцииПоиска();
	ОпцииПоиска.Отбор("ИмяПакета", ВидСравнения.Равно, ИмяПакета);

	Репозитории = МенеджерСущностей().Получить(Тип("СущностьЗависимыйРепозиторий"), ОпцииПоиска);

	Идентификаторы = Новый Массив();
	Для Каждого Элемент Из Репозитории Цикл
		Идентификаторы.Добавить(Элемент.ИдентификаторРепозитория);
	КонецЦикла;

	Возврат _ХранилищеРепозиториев.ПолучитьРепозитории(Идентификаторы, Сортировка);

КонецФункции

// Возвращает список имен пакетов, требующих обновления на указанную дату.
//
// Параметры:
//   НаДату - Дата - Дата, на которую необходимо определить актуальные пакеты для обновления
//   РазмерВыборки - Число - Максимальное количество возвращаемых пакетов
//
// Возвращаемое значение:
//   Массив из Строка
Функция ПолучитьПакетыДляОбновления(НаДату, РазмерВыборки) Экспорт
		
	ДатаОкончания = РаботаСДаннымиБД.НормализоватьУниверсальнуюДату(НаДату);

	ОпцииПоиска = Новый ОпцииПоиска();
	ОпцииПоиска.Первые(РазмерВыборки);
	ОпцииПоиска.Отбор("ДатаОбновления", ВидСравнения.МеньшеИлиРавно, ДатаОкончания);
	ОпцииПоиска.СортироватьПо("ДатаОбновления", НаправлениеСортировки.Возр);

	СтатистикаПакетов = МенеджерСущностей().Получить(Тип("СущностьСтатистикаПакета"), ОпцииПоиска);

	Результат = Новый Массив();
	Для Каждого СтатистикаПакета Из СтатистикаПакетов Цикл
		Результат.Добавить(СтатистикаПакета.ИмяПакета);
	КонецЦикла;

	Возврат Результат;

КонецФункции

// Проверяет есть ли данные по пакету 
//
// Параметры:
//   ИмяПакета - Строка - Имя пакета
//
// Возвращаемое значение:
//   Булево
Функция ЕстьДанныеПоПакету(ИмяПакета) Экспорт
	Возврат КоличествоРепозиториев(ИмяПакета) <> Неопределено;
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ОбновитьСтатистикуПакета(ИмяПакета, КоличествоОбратныхЗависимостей)
	
	ОпцииПоиска = Новый ОпцииПоиска();
	ОпцииПоиска.Отбор("ИмяПакета", ВидСравнения.Равно, ИмяПакета);

	СтатистикаПакета = МенеджерСущностей().ПолучитьОдно(Тип("СущностьСтатистикаПакета"), ОпцииПоиска);
	Если СтатистикаПакета = Неопределено Тогда
		СтатистикаПакета = Новый СущностьСтатистикаПакета();
		СтатистикаПакета.ИмяПакета = ИмяПакета;
		СтатистикаПакета.ДатаДобавления = РаботаСДаннымиБД.НормализоватьУниверсальнуюДату(ТекущаяУниверсальнаяДата());
	КонецЕсли;

	СтатистикаПакета.ДатаОбновления = РаботаСДаннымиБД.НормализоватьУниверсальнуюДату(ТекущаяУниверсальнаяДата());
	СтатистикаПакета.КоличествоОбратныхЗависимостей = КоличествоОбратныхЗависимостей;
	
	МенеджерСущностей().Сохранить(СтатистикаПакета);

КонецПроцедуры

Процедура ОбновитьЗависимыеРепозитории(ИмяПакета, ЗависимыеРепозитории)

	УдалитьЗависимыеРепозитории(ИмяПакета);

	Для Каждого ОписаниеРепозитория Из ЗависимыеРепозитории Цикл

		Если Не ЗначениеЗаполнено(ОписаниеРепозитория.Идентификатор) Тогда
			Продолжить;
		КонецЕсли;

		ЗависимыйРепозиторий = Новый СущностьЗависимыйРепозиторий();
		ЗависимыйРепозиторий.ИдентификаторРепозитория = ОписаниеРепозитория.Идентификатор;
		ЗависимыйРепозиторий.ИмяПакета = ИмяПакета;

		МенеджерСущностей().Сохранить(ЗависимыйРепозиторий);

	КонецЦикла;
	
КонецПроцедуры

Процедура УдалитьЗависимыеРепозитории(ИмяПакета)

	ОпцииПоиска = Новый ОпцииПоиска();
	ОпцииПоиска.Отбор("ИмяПакета", ВидСравнения.Равно, ИмяПакета);

	Сущности = МенеджерСущностей().Получить(Тип("СущностьЗависимыйРепозиторий"), ОпцииПоиска);
	Для Каждого Сущность Из Сущности Цикл
		МенеджерСущностей().Удалить(Сущность);
	КонецЦикла;
	
КонецПроцедуры

Процедура ДобавитьНовыеРепозитории(ЗависимыеРепозитории)

	Идентификаторы = Новый Массив();
	КартаЗависимыхРепозиториев = Новый КартаСоответствие();
	Для Каждого ОписаниеРепозитория Из ЗависимыеРепозитории Цикл
		Если ЗначениеЗаполнено(ОписаниеРепозитория.Идентификатор) Тогда
			Идентификаторы.Добавить(ОписаниеРепозитория.Идентификатор);
			КартаЗависимыхРепозиториев.Вставить(ОписаниеРепозитория.Идентификатор, ОписаниеРепозитория);
		КонецЕсли;
	КонецЦикла;

	НайденныеРепозитории = _ХранилищеРепозиториев.ПолучитьРепозитории(Идентификаторы);
	Для Каждого СущностьРепозиторий Из НайденныеРепозитории Цикл
		КартаЗависимыхРепозиториев.Удалить(СущностьРепозиторий.Идентификатор);
	КонецЦикла;

	Для Каждого ОписаниеРепозитория Из КартаЗависимыхРепозиториев.Значения() Цикл

		СущностьРепозиторий = Новый СущностьРепозиторий();
		СущностьРепозиторий.Идентификатор = ОписаниеРепозитория.Идентификатор;
		СущностьРепозиторий.ПолноеИмя = ОписаниеРепозитория.ПолноеИмя;
		СущностьРепозиторий.URL = ОписаниеРепозитория.URL;
		СущностьРепозиторий.Владелец = ОписаниеРепозитория.Владелец;
		СущностьРепозиторий.ТребуетсяОбновление = Истина;

		_ХранилищеРепозиториев.Сохранить(СущностьРепозиторий);

	КонецЦикла;
	
КонецПроцедуры

Функция МенеджерСущностей()
	Возврат _МенеджерСущностей.Достать();
КонецФункции

#КонецОбласти