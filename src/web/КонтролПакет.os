#Использовать 1connector
#Использовать entity
#Использовать jason
#Использовать badge
#Использовать "../repository"
#Использовать "../service"

&Пластилин("ПроцессорОчередиПакетов")
Перем _ПроцессорОчередиПакетов; // ПроцессорОчередиПакетов

&Пластилин("ХранилищеЗависимыхРепозиториев")
Перем _ХранилищеЗависимыхРепозиториев; // ХранилищеЗависимыхРепозиториев

&Пластилин("МенеджерОтображений")
Перем _МенеджерОтображений; // МенеджерОтображений (см. winow)

&Пластилин("ВалидаторИмениПакета")
Перем _ВалидаторИмениПакета; // ВалидаторИмениПакета

Перем _Бейдж; // Бейдж (см. badge)

&Контроллер("/package")
Процедура ПриСозданииОбъекта()

	_Бейдж = Новый Бейдж();

КонецПроцедуры

#Область ТочкиМаршрута

&ТочкаМаршрута("{Значение}")
Процедура ТочкаПакета(Ответ, Значение) Экспорт

	Страница = РазобратьИмяСтраницы(Значение);
	ИмяПакета = РаботаСДаннымиБД.НормализироватьИмяПакета(Страница.ИмяПакета);
	Расширение = Страница.Расширение;

	РезультатПроверки = ПроверитьПакет(ИмяПакета);
	
	Если Не ЗначениеЗаполнено(Расширение) Тогда
		// Убрать 'КодироватьСтроку' при исправлении issue https://github.com/autumn-library/winow/issues/117
		Ответ.Перенаправить("/?package=" + КодироватьСтроку(ИмяПакета, СпособКодированияСтроки.URLВКодировкеURL));
	ИначеЕсли Расширение = "svg" Тогда
		ОтветБейдж(ИмяПакета, РезультатПроверки, Ответ);
	ИначеЕсли Расширение = "json" Тогда
		ОтветJson(ИмяПакета, РезультатПроверки, Ответ);
	Иначе
		Ответ404(РезультатПроверки, Ответ);
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ПроверитьПакет(ИмяПакета)
		
	РезультатПроверки = Новый Структура("ЕстьОшибки, ТекстСообщения", Ложь, "");

	РезультатПроверки.ТекстСообщения = _ВалидаторИмениПакета.Валидировать(ИмяПакета);
	РезультатПроверки.ЕстьОшибки = ЗначениеЗаполнено(РезультатПроверки.ТекстСообщения);

	Если РезультатПроверки.ЕстьОшибки Тогда
		Возврат РезультатПроверки;
	КонецЕсли;

	Если Не _ХранилищеЗависимыхРепозиториев.ЕстьДанныеПоПакету(ИмяПакета) Тогда
		РезультатПроверки.ТекстСообщения = 
			"Данные по пакету пока отсутствуют. Сбор информации поставлен в очередь - результаты будут доступны позже.";
		_ПроцессорОчередиПакетов.ДобавитьВОчередь(ИмяПакета);
	КонецЕсли;

	Возврат РезультатПроверки;

КонецФункции

Функция РазобратьИмяСтраницы(Значение)

	Части = СтрРазделить(Значение, ".", Ложь);
	Если Части.Количество() = 2 Тогда
		ИмяПакета = Части[0];
		Расширение = НРег(Части[1]);
	Иначе
		ИмяПакета = Значение;
		Расширение = "";
	КонецЕсли;

	Возврат Новый Структура("ИмяПакета, Расширение", ИмяПакета, Расширение);

КонецФункции

Процедура ОтветБейдж(ИмяПакета, РезультатПроверки, Ответ)

	КоличествоЗависимыхРепозиториев = 0;

	Если Не ЗначениеЗаполнено(РезультатПроверки.ТекстСообщения) Тогда
		КоличествоЗависимыхРепозиториев = _ХранилищеЗависимыхРепозиториев.КоличествоРепозиториев(ИмяПакета);
	КонецЕсли;
	
	Ответ.Заголовки["Content-Type"] = "image/svg+xml";
	Ответ.ТелоТекст = ПолучитьБейдж(КоличествоЗависимыхРепозиториев, РезультатПроверки.ТекстСообщения);

КонецПроцедуры

Процедура ОтветJson(ИмяПакета, РезультатПроверки, Ответ)

	Сортировка = "КоличествоЗвезд Убыв, ЭтоФорк, Имя";

	Результат = Новый Структура();
	Результат.Вставить("package_name", ИмяПакета);
	Результат.Вставить("repositories", Новый Массив());

	Ответ.УстановитьТипКонтента("json");		
	
	Если ЗначениеЗаполнено(РезультатПроверки.ТекстСообщения) Тогда
		Если РезультатПроверки.ЕстьОшибки Тогда
			Результат.Вставить("error", РезультатПроверки.ТекстСообщения);
		Иначе
			Результат.Вставить("message", РезультатПроверки.ТекстСообщения);
		КонецЕсли;
	Иначе
		Результат["repositories"] = _ХранилищеЗависимыхРепозиториев.ПолучитьРепозитории(ИмяПакета, Сортировка);
	КонецЕсли;

	СериализаторJson = Новый СериализаторJson();
	
	Ответ.ТелоТекст = СериализаторJson.Сериализовать(Результат);

КонецПроцедуры

Процедура Ответ404(РезультатПроверки, Ответ)

	ТекстСообщения = ?(ЗначениеЗаполнено(РезультатПроверки.ТекстСообщения), 
		РезультатПроверки.ТекстСообщения,
		"Страница не найдена");

	Ответ.УстановитьСостояниеНеНайдено();
	Ответ.УстановитьТекстШаблона(_МенеджерОтображений.ПолучитьОтображение(404));

	Ответ.Модель = Новый Структура();
	Ответ.Модель.Вставить("КодСостояния", 404);
	Ответ.Модель.Вставить("ТекстСообщения", ТекстСообщения);

КонецПроцедуры

Функция ПолучитьБейдж(КоличествоЗависимыхРепозиториев, ТекстСообщения)

	Если ЗначениеЗаполнено(ТекстСообщения) Тогда
		Значение = ТекстСообщения;
		Цвет = "red";
	ИначеЕсли КоличествоЗависимыхРепозиториев = Неопределено Тогда
		Значение = "pending";
		Цвет = "red";
	Иначе
		Значение = СтрШаблон("%1 repos", Формат(КоличествоЗависимыхРепозиториев, "ЧН=0; ЧГ="));
		Цвет = "#65ADF1";
	КонецЕсли;

	Конфигурация = Новый КонфигурацияБейджа("Used by", Значение, Цвет);
	Конфигурация.Логотип = "onescript";

	_Бейдж.ПрименитьКонфигурацию(Конфигурация);

	Возврат _Бейдж.ПолучитьSVG();

КонецФункции

#КонецОбласти