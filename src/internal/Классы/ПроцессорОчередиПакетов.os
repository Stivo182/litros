#Использовать collectionos
#Использовать entity
#Использовать autumn-logos
#Использовать "../../dto"

&Табакерка
&Пластилин("МенеджерСущностей")
Перем _МенеджерСущностей; // Табакерка - МенеджерСущностей (см. entity)

&Пластилин("ОбновляторЗависимыхРепозиториев")
Перем _ОбновляторЗависимыхРепозиториев; // ОбновляторЗависимыхРепозиториев

&Лог("litros.ПроцессорОчередиПакетов")
Перем _Лог; // Лог

&Деталька(Значение = "litros.ИнтервалОбработкиОчереди", ЗначениеПоУмолчанию = 60) 
Перем _ИнтервалОбработкиОчереди; // Число

&Желудь
Процедура ПриСозданииОбъекта()
КонецПроцедуры

#Область ПрограммныйИнтерфейс

Процедура Запустить() Экспорт

	РазмерВыборки = 10;

	_Лог.Отладка("Запуск обработки очереди пакетов");
	
	ОпцииПоиска = Новый ОпцииПоиска();
	ОпцииПоиска.Первые(РазмерВыборки);
	ОпцииПоиска.СортироватьПо("ДатаДобавления", НаправлениеСортировки.Возр);

	Пока Истина Цикл

		Попытка
			ОчередьПакетов = МенеджерСущностей().Получить(Тип("СущностьОчередьПакета"), ОпцииПоиска);
			Для Каждого ОчередьПакета Из ОчередьПакетов Цикл
				ОбработатьПакет(ОчередьПакета);
			КонецЦикла;
		Исключение
			ТекстОшибки = СтрШаблон("Возникла ошибка при обработке очереди пакетов: 
			|%1", ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			_Лог.Ошибка(ТекстОшибки);
		КонецПопытки;

		Приостановить(_ИнтервалОбработкиОчереди * 1000);

	КонецЦикла;

КонецПроцедуры

Процедура ДобавитьВОчередь(ИмяПакета) Экспорт

	ОпцииПоиска = Новый ОпцииПоиска();
	ОпцииПоиска.Отбор("ИмяПакета", ВидСравнения.Равно, ИмяПакета);

	ОчередьПакета = МенеджерСущностей().ПолучитьОдно(Тип("СущностьОчередьПакета"), ОпцииПоиска);
	Если Не ОчередьПакета = Неопределено Тогда
		Возврат;
	КонецЕсли;

	ОчередьПакета = Новый СущностьОчередьПакета();
	ОчередьПакета.ИмяПакета = ИмяПакета;	
	ОчередьПакета.ДатаДобавления = РаботаСДаннымиБД.НормализоватьУниверсальнуюДату(ТекущаяУниверсальнаяДата());

	МенеджерСущностей().Сохранить(ОчередьПакета);
		
	_Лог.Отладка("Пакет %1 добавлен в очередь", ИмяПакета);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ОбработатьПакет(ОчередьПакета)

	ИмяПакета = ОчередьПакета.ИмяПакета;
	Успех = Ложь;

	Попытка
		Результат = _ОбновляторЗависимыхРепозиториев.ОбновитьПакет(ИмяПакета);
		Успех = Результат.Успех;
	Исключение
		ТекстОшибки = СтрШаблон("Не удалось обновить статистику пакета '%1': 
		|%2", ИмяПакета, ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		_Лог.Ошибка(ТекстОшибки);
	КонецПопытки;

	Если Успех Тогда
		МенеджерСущностей().Удалить(ОчередьПакета);
	КонецЕсли;

КонецПроцедуры

Функция МенеджерСущностей()
	Возврат _МенеджерСущностей.Достать();
КонецФункции

#КонецОбласти