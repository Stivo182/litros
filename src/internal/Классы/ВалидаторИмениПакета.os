Перем _МаксимальнаяДлина; // Число
Перем _РегулярноеВыражение; // РегулярноеВыражение

&Желудь
Процедура ПриСозданииОбъекта()

	_МаксимальнаяДлина = 40;

	_РегулярноеВыражение = Новый РегулярноеВыражение("^[a-zA-Z0-9_-]+$");
	_РегулярноеВыражение.Многострочный = Ложь;
	_РегулярноеВыражение.ИгнорироватьРегистр = Истина;

КонецПроцедуры

Функция Валидировать(ИмяПакета) Экспорт

	Если Не ЗначениеЗаполнено(ИмяПакета) Тогда
		Возврат "Имя пакета не указано";
	КонецЕсли;

	Если СтрДлина(ИмяПакета) > _МаксимальнаяДлина Тогда
		Возврат СтрШаблон("Длина имени пакета не должна превышать %1 символов", _МаксимальнаяДлина);
	КонецЕсли;

	Если Не _РегулярноеВыражение.Совпадает(ИмяПакета) Тогда
		Возврат "Имя пакета содержит недопустимые символы";
	КонецЕсли;

	Возврат "";

КонецФункции