#Использовать entity
#Использовать autumn-logos
#Использовать "../../repository"

&Пластилин("СканерИспользованийПакета")
Перем _СканерИспользованийПакета; // СканерИспользованийПакета

&Пластилин("ХранилищеЗависимыхРепозиториев")
Перем _ХранилищеЗависимыхРепозиториев; // ХранилищеЗависимыхРепозиториев

&Лог("litros.ОбновляторСтатистикиПакетов")
Перем _Лог; // Лог

&Деталька(Значение = "litros.ИнтервалПроверкиАктуальности", ЗначениеПоУмолчанию = 300) 
Перем _ИнтервалПроверкиАктуальности; // Число

&Деталька(Значение = "litros.СрокАктуальностиПакета", ЗначениеПоУмолчанию = 86400) 
Перем _СрокАктуальностиПакета; // Число

&Желудь
Процедура ПриСозданииОбъекта()
КонецПроцедуры

#Область ПрограммныйИнтерфейс

Функция ОбновитьПакет(ИмяПакета) Экспорт

	Результат = Новый Структура();
	Результат.Вставить("Успех", Ложь);
	Результат.Вставить("КоличествоОбратныхЗависимостей", 0);

	_Лог.Отладка("Обновление статистики пакета %1", ИмяПакета);

	РезультатПоиска = _СканерИспользованийПакета.НайтиРепозитории(ИмяПакета);
	Если Не РезультатПоиска.Завершено Тогда
		_Лог.Отладка("Не удалось обновить статистику пакета %1", ИмяПакета);
		Возврат Результат;
	КонецЕсли;

	_ХранилищеЗависимыхРепозиториев.Сохранить(ИмяПакета, РезультатПоиска.Данные);

	_Лог.Отладка("Успешное обновление статистики пакета %1", ИмяПакета);

	Результат.Успех = Истина;
	Результат.КоличествоОбратныхЗависимостей = РезультатПоиска.Данные.Количество();

	Возврат Результат;

КонецФункции

Процедура ОбновитьУстаревшиеПакеты(РазмерВыборки) Экспорт
	
	ДатаОкончания = ТекущаяУниверсальнаяДата() - _СрокАктуальностиПакета;

	_Лог.Отладка("Запуск обновления статистики всех пакетов");
	
	Пакеты = _ХранилищеЗависимыхРепозиториев.ПолучитьПакетыДляОбновления(ДатаОкончания, РазмерВыборки);

	Для Каждого ИмяПакета Из Пакеты Цикл
		Попытка
			ОбновитьПакет(ИмяПакета);
		Исключение
			ТекстОшибки = СтрШаблон("Не удалось обновить статистику пакета '%1': 
			|%2", ИмяПакета, ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			_Лог.Ошибка(ТекстОшибки);
		КонецПопытки;
	КонецЦикла;

	_Лог.Отладка("Завершение обновления статистики всех пакетов");
	
КонецПроцедуры

Процедура ЗапуститьИнтервальноеОбновлениеПакетов() Экспорт

	РазмерВыборки = 10;
	МиллисекундВСекунде = 1000;

	_Лог.Отладка("Запуск интервального обновления статистики всех пакетов");
	
	Пока Истина Цикл

		Попытка
			ОбновитьУстаревшиеПакеты(РазмерВыборки);
		Исключение
			ТекстОшибки = СтрШаблон("Не удалось обновить статистику пакетов: 
			|%1", ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			_Лог.Ошибка(ТекстОшибки);	
		КонецПопытки;

		Приостановить(_ИнтервалПроверкиАктуальности * МиллисекундВСекунде);

	КонецЦикла;

КонецПроцедуры

#КонецОбласти