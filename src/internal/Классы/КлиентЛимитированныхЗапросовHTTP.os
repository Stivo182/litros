#Использовать 1connector

&Лог("litros.КлиентЛимитированныхЗапросовHTTP")
Перем _Лог; // Лог

Перем _Блокировка; // БлокировкаРесурса
Перем _ДатаСброса; // Дата
Перем _ОстатокЛимита; // Число

&Желудь
&Характер("Компанейский")
Процедура ПриСозданииОбъекта()

	_Блокировка = Новый БлокировкаРесурса(ЭтотОбъект);
	_ДатаСброса = '00010101';
	_ОстатокЛимита = 0;

КонецПроцедуры

#Область ПрограммныйИнтерфейс

Функция Get(URL, ПараметрыЗапроса = Неопределено, ДополнительныеПараметры = Неопределено) Экспорт

	Если ДополнительныеПараметры = Неопределено Тогда
		ДополнительныеПараметры = Новый Структура();
	КонецЕсли;

	Если Не ПараметрыЗапроса = Неопределено Тогда
		ДополнительныеПараметры.Вставить("ПараметрыЗапроса", ПараметрыЗапроса);
	КонецЕсли;

	Возврат ВызватьМетод("GET", URL, ДополнительныеПараметры);

КонецФункции

Функция Post(URL, Данные = Неопределено, Json = Неопределено, ДополнительныеПараметры = Неопределено) Экспорт

	Если ДополнительныеПараметры = Неопределено Тогда
		ДополнительныеПараметры = Новый Структура();
	КонецЕсли;

	Если Не Данные = Неопределено Тогда
		ДополнительныеПараметры.Вставить("Данные", Данные);
	КонецЕсли;

	Если Не Json = Неопределено Тогда
		ДополнительныеПараметры.Вставить("Json", Json);
	КонецЕсли;

	Возврат ВызватьМетод("POST", URL, ДополнительныеПараметры);

КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ВызватьМетод(Метод, URL, ДополнительныеПараметры)

	_Лог.Отладка("Отправка %1 запроса на %2", Метод, URL);

	_Блокировка.Заблокировать();

	Если Не МожноВыполнитьЗапрос() Тогда

		_Лог.Отладка("Отменена отправка %1 запроса на %2. Остаток лимита: %3, дата сброса: %4",
			Метод, URL, _ОстатокЛимита, _ДатаСброса);

		_Блокировка.Разблокировать();

		Возврат Неопределено;

	КонецЕсли;

	Попытка	
		Ответ = КоннекторHTTP.ВызватьМетод(Метод, URL, ДополнительныеПараметры);
	Исключение
		_Блокировка.Разблокировать();
		ВызватьИсключение;
	КонецПопытки;

	ОбновитьЛимитыИзОтвета(Ответ);

	_Блокировка.Разблокировать();

	_Лог.Отладка("Завершен %1 запрос на %2. Остаток лимита: %3, дата сброса: %4",
		Метод, URL, _ОстатокЛимита, _ДатаСброса);

	Возврат Ответ;
	
КонецФункции

Функция МожноВыполнитьЗапрос()
	Возврат _ОстатокЛимита > 0 Или _ДатаСброса < ТекущаяУниверсальнаяДата();
КонецФункции

Процедура ОбновитьЛимитыИзОтвета(Ответ)
	
	ОбновитьОстатокЛимита(Ответ);
	ОбновитьДатуСброса(Ответ);

КонецПроцедуры

Процедура ОбновитьОстатокЛимита(Ответ)

	_ОстатокЛимита = 0;
	
	Остаток = Ответ.Заголовки["X-RateLimit-Remaining"];
	Если Не Остаток = Неопределено Тогда
		_ОстатокЛимита = Макс(Число(Остаток) - 1, 0);
	КонецЕсли;

КонецПроцедуры

Процедура ОбновитьДатуСброса(Ответ)

	_ДатаСброса = '00010101';
	
	ДатаСброса = Ответ.Заголовки["X-RateLimit-Reset"];
	Если Не ДатаСброса = Неопределено Тогда
		_ДатаСброса = TimestampВДату(ДатаСброса);
	КонецЕсли;	

КонецПроцедуры

Функция TimestampВДату(Значение) Экспорт
	Возврат Дата(1970, 1, 1) + Число(Значение);
КонецФункции

#КонецОбласти