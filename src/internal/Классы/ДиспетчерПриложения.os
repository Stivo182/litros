Перем _ПроцессорОчередиПакетов;         // ПроцессорОчередиПакетов
Перем _ОбновляторЗависимыхРепозиториев; // ОбновляторЗависимыхРепозиториев
Перем _ОбновляторРепозиториев;          // ОбновляторРепозиториев
Перем _ЗапускательВебПриложения;        // ЗапускательВебПриложения (см. winow)
Перем _НастройкиВебПриложения;          // Настройки (см. winow)

&Желудь
Процедура ПриСозданииОбъекта(
	&Пластилин ПроцессорОчередиПакетов,
	&Пластилин ОбновляторЗависимыхРепозиториев,
	&Пластилин ОбновляторРепозиториев,
	&Пластилин ЗапускательВебПриложения,
	&Пластилин("Настройки") НастройкиВебПриложения
)

	_ПроцессорОчередиПакетов = ПроцессорОчередиПакетов;
	_ОбновляторЗависимыхРепозиториев = ОбновляторЗависимыхРепозиториев;
	_ОбновляторРепозиториев = ОбновляторРепозиториев;
	_ЗапускательВебПриложения = ЗапускательВебПриложения;
	_НастройкиВебПриложения = НастройкиВебПриложения;

КонецПроцедуры

#Область ПрограммныйИнтерфейс

Процедура Запустить(Порт = 0) Экспорт

	ЗапуститьОбработкуОчереди();
	ЗапуститьОбновляторЗависимыхРепозиториев();
	ЗапуститьОбновляторРепозиториев();
	ЗапуститьВебПриложение(Порт);

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ЗапуститьОбработкуОчереди()
	ФоновыеЗадания.Выполнить(_ПроцессорОчередиПакетов, "Запустить");
КонецПроцедуры

Процедура ЗапуститьОбновляторЗависимыхРепозиториев()
	ФоновыеЗадания.Выполнить(_ОбновляторЗависимыхРепозиториев, "ЗапуститьИнтервальноеОбновлениеПакетов");
КонецПроцедуры

Процедура ЗапуститьОбновляторРепозиториев()
	ФоновыеЗадания.Выполнить(_ОбновляторРепозиториев, "ЗапуститьИнтервальноеОбновлениеРепозиториев");
КонецПроцедуры

Процедура ЗапуститьВебПриложение(Порт = 0)		

	Если ЗначениеЗаполнено(Порт) Тогда
		_НастройкиВебПриложения.Порт = Порт;
	КонецЕсли;

	_ЗапускательВебПриложения.Запустить();

КонецПроцедуры

#КонецОбласти