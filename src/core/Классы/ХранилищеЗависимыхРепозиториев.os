#Использовать entity
#Использовать "../../dto"
#Использовать "../../internal"

&Пластилин("МенеджерСущностей")
Перем _МенеджерСущностей; // МенеджерСущностей (см. entity)

&Желудь
Процедура ПриСозданииОбъекта()

КонецПроцедуры

#Область ПрограммныйИнтерфейс

// Сохраняет репозитории, зависящих от пакета
//
// Параметры:
//   ИмяПакета - Строка - Имя пакета
//   Массив из Структура - Список репозиториев:
//     * URL - Строка - URL репозитория
//     * ПолноеИмя - Строка - Полное имя репозитория
//     * Владелец - Строка - Логин владельца
//     * ЭтоФорк - Булево - Признак форка
Процедура Сохранить(ИмяПакета, ЗависимыеРепозитории) Экспорт
		
	КоличествоОбратныхЗависимостей = ЗависимыеРепозитории.Количество();

	_МенеджерСущностей.НачатьТранзакцию();

	ЗаписатьСтатистикуПакета(ИмяПакета, КоличествоОбратныхЗависимостей);
	ЗаписатьЗависимыеРепозитории(ИмяПакета, ЗависимыеРепозитории);

	_МенеджерСущностей.ЗафиксироватьТранзакцию();

КонецПроцедуры

// Возвращает количество репозиториев, зависящих от пакета 
//
// Параметры:
//   ИмяПакета - Строка - Имя пакета
//
// Возвращаемое значение:
//   Число, Неопределено
Функция КоличествоРепозиториев(ИмяПакета) Экспорт

	ОпцииПоиска = Новый ОпцииПоиска();
	ОпцииПоиска.Отбор("ИмяПакета", ВидСравнения.Равно, ИмяПакета);

	СтатистикаПакета = _МенеджерСущностей.ПолучитьОдно(Тип("СущностьСтатистикаПакета"), ОпцииПоиска);

	Если Не СтатистикаПакета = Неопределено Тогда
		Возврат СтатистикаПакета.КоличествоОбратныхЗависимостей;
	КонецЕсли;

КонецФункции

// Возвращает описания репозиториев, зависящих от пакета
//
// Параметры:
//   ИмяПакета - Строка - Имя пакета
//
// Возвращаемое значение:
//   Массив из СущностьЗависимыйРепозиторий
Функция ПолучитьСписокРепозиториев(ИмяПакета) Экспорт

	Результат = Новый Массив();
	
	ОпцииПоиска = Новый ОпцииПоиска();
	ОпцииПоиска.Отбор("ИмяПакета", ВидСравнения.Равно, ИмяПакета);
	ОпцииПоиска.СортироватьПо("ЭтоФорк", НаправлениеСортировки.Возр);
	ОпцииПоиска.СортироватьПо("ИмяПакета", НаправлениеСортировки.Возр);

	Репозитории = _МенеджерСущностей.Получить(Тип("СущностьЗависимыйРепозиторий"), ОпцииПоиска);

	Для Каждого Элемент Из Репозитории Цикл
		Если ТипЗнч(Элемент) = Тип("Сценарий") Тогда
			Сущность = Новый СущностьЗависимыйРепозиторий();
			ЗаполнитьЗначенияСвойств(Сущность, Элемент);
			Результат.Добавить(Сущность);
		Иначе
			Результат.Добавить(Элемент);
		КонецЕсли;
	КонецЦикла;

	Возврат Результат;

КонецФункции

// Возвращает список имен пакетов, требующих обновления на указанную дату.
//
// Параметры:
//   НаДату - Дата - Дата, на которую необходимо определить актуальные пакеты для обновления
//   РазмерВыборки - Число - Максимальное количество возвращаемых пакетов
//
// Возвращаемое значение:
//   Массив из Строка
Функция ПолучитьПакетыДляОбновления(НаДату, РазмерВыборки) Экспорт
		
	ДатаОкончания = РаботаСДаннымиБД.НормализоватьУниверсальнуюДату(НаДату);

	ОпцииПоиска = Новый ОпцииПоиска();
	ОпцииПоиска.Первые(РазмерВыборки);
	ОпцииПоиска.Отбор("ДатаОбновления", ВидСравнения.МеньшеИлиРавно, ДатаОкончания);
	ОпцииПоиска.СортироватьПо("ДатаОбновления", НаправлениеСортировки.Возр);

	СтатистикаПакетов = _МенеджерСущностей.Получить(Тип("СущностьСтатистикаПакета"), ОпцииПоиска);

	Результат = Новый Массив();
	Для Каждого СтатистикаПакета Из СтатистикаПакетов Цикл
		Результат.Добавить(СтатистикаПакета.ИмяПакета);
	КонецЦикла;

	Возврат Результат;

КонецФункции

// Проверяет есть ли данные по пакету 
//
// Параметры:
//   ИмяПакета - Строка - Имя пакета
//
// Возвращаемое значение:
//   Булево
Функция ЕстьДанныеПоПакету(ИмяПакета) Экспорт
	Возврат КоличествоРепозиториев(ИмяПакета) <> Неопределено;
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ЗаписатьСтатистикуПакета(ИмяПакета, КоличествоОбратныхЗависимостей)
	
	ОпцииПоиска = Новый ОпцииПоиска();
	ОпцииПоиска.Отбор("ИмяПакета", ВидСравнения.Равно, ИмяПакета);

	СтатистикаПакета = _МенеджерСущностей.ПолучитьОдно(Тип("СущностьСтатистикаПакета"), ОпцииПоиска);
	Если СтатистикаПакета = Неопределено Тогда
		СтатистикаПакета = Новый СущностьСтатистикаПакета();
		СтатистикаПакета.ИмяПакета = ИмяПакета;
		СтатистикаПакета.ДатаДобавления = РаботаСДаннымиБД.НормализоватьУниверсальнуюДату(ТекущаяУниверсальнаяДата());
	КонецЕсли;

	СтатистикаПакета.ДатаОбновления = РаботаСДаннымиБД.НормализоватьУниверсальнуюДату(ТекущаяУниверсальнаяДата());
	СтатистикаПакета.КоличествоОбратныхЗависимостей = КоличествоОбратныхЗависимостей;
	
	_МенеджерСущностей.Сохранить(СтатистикаПакета);

КонецПроцедуры

Процедура ЗаписатьЗависимыеРепозитории(ИмяПакета, Репозитории)

	УдалитьЗависимыеРепозитории(ИмяПакета);

	Для Каждого Описание Из Репозитории Цикл

		ЗависимыйРепозиторий = Новый СущностьЗависимыйРепозиторий();
		ЗависимыйРепозиторий.ИмяПакета = ИмяПакета;
		ЗависимыйРепозиторий.URL = Описание.URL;
		ЗависимыйРепозиторий.ПолноеИмя = Описание.ПолноеИмя;
		ЗависимыйРепозиторий.Владелец = Описание.Владелец;
		ЗависимыйРепозиторий.ЭтоФорк = Описание.ЭтоФорк;

		_МенеджерСущностей.Сохранить(ЗависимыйРепозиторий);

	КонецЦикла;
	
КонецПроцедуры

Процедура УдалитьЗависимыеРепозитории(ИмяПакета)

	ОпцииПоиска = Новый ОпцииПоиска();
	ОпцииПоиска.Отбор("ИмяПакета", ВидСравнения.Равно, ИмяПакета);

	Сущности = _МенеджерСущностей.Получить(Тип("СущностьЗависимыйРепозиторий"), ОпцииПоиска);
	Для Каждого Сущность Из Сущности Цикл
		_МенеджерСущностей.Удалить(Сущность);
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти