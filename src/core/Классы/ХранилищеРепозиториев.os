#Использовать entity
#Использовать "../../dto"
#Использовать "../../internal"

&Пластилин("МенеджерСущностей")
Перем _МенеджерСущностей; // МенеджерСущностей (см. entity)

&Желудь
Процедура ПриСозданииОбъекта()

КонецПроцедуры

#Область ПрограммныйИнтерфейс

// Сохраняет репозиторий
//
// Параметры:
//   Сущность - СущностьРепозиторий
Процедура Сохранить(Сущность) Экспорт

	Сущность.ДатаОбновления = РаботаСДаннымиБД.НормализоватьУниверсальнуюДату(ТекущаяУниверсальнаяДата());

	_МенеджерСущностей.Сохранить(Сущность);

КонецПроцедуры

// Возвращает описания репозиториев
//
// Параметры:
//   ПолныеИмена - Массив из Строка - Полные имена репозиториев
//   Сортировка - Строка - Список имён полей с указанием направления сортировки, разделённых запятыми
//
// Параметры:
//   Массив Из СущностьРепозиторий
Функция ПолучитьРепозитории(ПолныеИмена, Сортировка = "") Экспорт

	ОпцииПоиска = Новый ОпцииПоиска();
	ОпцииПоиска.Отбор("ПолноеИмя", ВидСравнения.В, ПолныеИмена);

	Для Каждого СтрокаСортировки Из СтрРазделить(Сортировка, ",") Цикл
		
		Части = СтрРазделить(СтрокаСортировки, " ", Ложь);
		КоличествоЧастей = Части.Количество();
		ИмяКолонки = "";
		Направление = "";

		Если КоличествоЧастей = 1 Тогда
			ИмяКолонки = СокрЛП(СтрокаСортировки);
			Направление = НаправлениеСортировки.Возр;
		ИначеЕсли КоличествоЧастей > 1 Тогда
			ИмяКолонки = СокрЛП(Части[0]);
			Направление = НаправлениеСортировки[СокрЛП(Части[1])];
		Иначе
			Продолжить;
		КонецЕсли;

		ОпцииПоиска.СортироватьПо(ИмяКолонки, Направление);

	КонецЦикла;
	
	Результат = Новый Массив();
	Сущности = _МенеджерСущностей.Получить(Тип("СущностьРепозиторий"), ОпцииПоиска);

	Для Каждого Сущность Из Сущности Цикл
		Если ТипЗнч(Сущность) = Тип("Сценарий") Тогда
			ПравильнаяСущность = Новый СущностьРепозиторий();
			ЗаполнитьЗначенияСвойств(ПравильнаяСущность, Сущность);
			Результат.Добавить(ПравильнаяСущность);
		Иначе
			Результат.Добавить(Сущность);
		КонецЕсли;
	КонецЦикла;

	Возврат Результат;

КонецФункции

// Возвращает список полных имен репозиториев, требующих обновления на указанную дату.
//
// Параметры:
//   НаДату - Дата - Дата, на которую необходимо определить репозитории для обновления
//   РазмерВыборки - Число - Максимальное количество возвращаемых репозиториев
//
// Возвращаемое значение:
//   Массив из Строка
Функция ПолучитьРепозиторииДляОбновления(НаДату, РазмерВыборки) Экспорт
		
	ДатаОкончания = РаботаСДаннымиБД.НормализоватьУниверсальнуюДату(НаДату);

	Результат = ПолучитьНеобработанныеРепозитории(РазмерВыборки);

	Если Результат.Количество() < РазмерВыборки Тогда

		РазмерВыборкиБезНеобработанных = РазмерВыборки - Результат.Количество();

		ОпцииПоиска = Новый ОпцииПоиска();
		ОпцииПоиска.Первые(РазмерВыборкиБезНеобработанных);
		ОпцииПоиска.Отбор("ДатаОбновления", ВидСравнения.МеньшеИлиРавно, ДатаОкончания);
		ОпцииПоиска.СортироватьПо("ДатаОбновления", НаправлениеСортировки.Возр);

		Репозитории = _МенеджерСущностей.Получить(Тип("СущностьРепозиторий"), ОпцииПоиска);

		Для Каждого Репозиторий Из Репозитории Цикл
			Результат.Добавить(Репозиторий.ПолноеИмя);
		КонецЦикла;

	КонецЕсли;

	Возврат Результат;

КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ПолучитьНеобработанныеРепозитории(РазмерВыборки)
	
	ТекстЗапроса = "SELECT DISTINCT
	|	ПолноеИмяРепозитория 
	|FROM 
	|	ЗависимыеРепозитории 
	|WHERE 
	|	NOT ЗависимыеРепозитории.ПолноеИмяРепозитория IN (
	|		SELECT 
	|			ПолноеИмя 
	|		FROM 
	|			Репозитории
	|		)
	|LIMIT %1";
	ТекстЗапроса = СтрШаблон(ТекстЗапроса, Формат(РазмерВыборки, "ЧГ="));

	Коннектор = _МенеджерСущностей.ПолучитьКоннектор();
	Таблица = Коннектор.ВыполнитьЗапрос(ТекстЗапроса);

	Возврат Таблица.ВыгрузитьКолонку("ПолноеИмяРепозитория");

КонецФункции

#КонецОбласти