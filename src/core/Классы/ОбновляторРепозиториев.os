#Использовать entity
#Использовать "../../dto"

&Пластилин("КлиентЛимитированныхЗапросовHTTP")
Перем _КлиентЛимитированныхЗапросовHTTP; // КлиентЛимитированныхЗапросовHTTP

&Пластилин("ХранилищеРепозиториев")
Перем _ХранилищеРепозиториев; // ХранилищеРепозиториев

&Лог("litros.ОбновляторРепозиториев")
Перем _Лог; // Лог

&Деталька("litros.ТокенGitHub") 
Перем _Токен; // Строка

&Деталька(Значение = "litros.ИнтервалПроверкиАктуальности", ЗначениеПоУмолчанию = 300) 
Перем _ИнтервалПроверкиАктуальности; // Число

&Деталька(Значение = "litros.СрокАктуальностиРепозитория", ЗначениеПоУмолчанию = 604800) 
Перем _СрокАктуальностиРепозитория; // Число

&Желудь
Процедура ПриСозданииОбъекта()
КонецПроцедуры

#Область ПрограммныйИнтерфейс

Процедура ОбновитьРепозитории(ПолныеИменаРепозиториев) Экспорт

	Если ПолныеИменаРепозиториев.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;

	_Лог.Отладка("Обновление репозиториев: %1", СтрРазделить(ПолныеИменаРепозиториев, ", "));

	Ответ = ПолучитьОписанияРепозиториевИзGitHub(ПолныеИменаРепозиториев);

	Если Ответ = Неопределено Тогда
		Возврат;
	КонецЕсли;

	ДанныеОтвета = Ответ.Json();

	Если ОбработатьОшибкиЗапросаКGitHub(Ответ, ДанныеОтвета) Тогда
		Возврат;
	КонецЕсли;

	Для Каждого Элемент Из ДанныеОтвета["data"] Цикл

		ДанныеРепозитория = Элемент.Значение;
		ПолноеИмя = СтрШаблон("%1/%2", ДанныеРепозитория["owner"]["login"], ДанныеРепозитория["name"]);

		СущностьРепозиторий = Новый СущностьРепозиторий();
		СущностьРепозиторий.Имя = ДанныеРепозитория["name"];
		СущностьРепозиторий.ПолноеИмя = ПолноеИмя;
		СущностьРепозиторий.URL = ДанныеРепозитория["url"];
		СущностьРепозиторий.Владелец = ДанныеРепозитория["owner"]["login"];
		СущностьРепозиторий.Описание = ДанныеРепозитория["description"];
		СущностьРепозиторий.ЭтоФорк = ДанныеРепозитория["isFork"];
		СущностьРепозиторий.КоличествоЗвезд = ДанныеРепозитория["stargazerCount"];
		СущностьРепозиторий.КоличествоФорков = ДанныеРепозитория["forkCount"];

		_ХранилищеРепозиториев.Сохранить(СущностьРепозиторий);

	КонецЦикла;

КонецПроцедуры

Процедура ОбновитьУстаревшиеРепозитории(РазмерВыборки) Экспорт
	
	ДатаОкончания = ТекущаяУниверсальнаяДата() - _СрокАктуальностиРепозитория;

	_Лог.Отладка("Запуск обновления репозиториев");
	
	ПолныеИменаРепозиториев = _ХранилищеРепозиториев.ПолучитьРепозиторииДляОбновления(ДатаОкончания, РазмерВыборки);
	ОбновитьРепозитории(ПолныеИменаРепозиториев);

	_Лог.Отладка("Завершение обновления репозиториев");
	
КонецПроцедуры

Процедура ЗапуститьИнтервальноеОбновлениеРепозиториев() Экспорт

	РазмерВыборки = 50;
	МиллисекундВСекунде = 1000;
	
	_Лог.Отладка("Запуск интервального обновления репозиториев");
	
	Пока Истина Цикл

		Попытка
			ОбновитьУстаревшиеРепозитории(РазмерВыборки);
		Исключение
			ТекстОшибки = СтрШаблон("Не удалось обновить репозитории: 
			|%1", ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			_Лог.Ошибка(ТекстОшибки);
		КонецПопытки;

		Приостановить(_ИнтервалПроверкиАктуальности * МиллисекундВСекунде);

	КонецЦикла;

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ПолучитьОписанияРепозиториевИзGitHub(ПолныеИменаРепозиториев)
	
	Заголовки = Новый Соответствие();
	Заголовки.Вставить("Authorization", "Bearer " + _Токен);

	ДополнительныеПараметры = Новый Структура();
	ДополнительныеПараметры.Вставить("Заголовки", Заголовки);

	Попытка
		Ответ = _КлиентЛимитированныхЗапросовHTTP.Post("https://api.github.com/graphql", , 
			ПодготовитьЗапросGraphQL(ПолныеИменаРепозиториев),
			ДополнительныеПараметры);
	Исключение
		_Лог.Ошибка(КраткоеПредставлениеОшибки(ИнформацияОбОшибке()));
		Возврат Неопределено;
	КонецПопытки;
	
	Возврат Ответ;

КонецФункции

Функция ПодготовитьЗапросGraphQL(ПолныеИменаРепозиториев)
	
	ЧастиЗапроса = Новый Массив();
	Для Каждого ПолноеИмя Из ПолныеИменаРепозиториев Цикл

		ЧастиИмени = СтрРазделить(ПолноеИмя, "/");
		Владелец = ЧастиИмени[0];
		ИмяРепозитория = ЧастиИмени[1];

		ЧастиЗапроса.Добавить(
			СтрШаблон("repo%1: repository(owner: ""%2"", name: ""%3"") {
			|	name
			|	description
			|	url
			|	isFork
			|	stargazerCount
			|	forkCount
			|	owner {
			|		login
			|	}
			|}", 
			Формат(ЧастиЗапроса.Количество() + 1, "ЧГ="),
			Владелец,
			ИмяРепозитория
		));

	КонецЦикла;

	ТекстЗапроса = СтрСоединить(ЧастиЗапроса, Символы.ПС);
	
	Возврат Новый Структура("query", СтрШаблон("query { %1 }", ТекстЗапроса));

КонецФункции

Функция ОбработатьОшибкиЗапросаКGitHub(Ответ, ДанныеОтвета)

	Если Ответ.КодСостояния <> 200 Тогда
		ТекстОшибки = Ответ.Текст();
	Иначе
		Ошибки = Новый Массив();

		Если ДанныеОтвета["errors"] <> Неопределено Тогда
			Для Каждого Ошибка Из ДанныеОтвета["errors"] Цикл
				Ошибки.Добавить(Ошибка["message"]);
			КонецЦикла;
		КонецЕсли;

		ТекстОшибки = СтрСоединить(Ошибки, Символы.ПС);
	КонецЕсли;

	Если Не ЗначениеЗаполнено(ТекстОшибки) Тогда
		Возврат Ложь;
	КонецЕсли;

	ТекстОшибки = СтрШаблон(
		"Не удалось получить информацию об репозиториях при выполнении запроса к Github: %1",
		ТекстОшибки);

	_Лог.Ошибка(ТекстОшибки);

	Возврат Истина;

КонецФункции

#КонецОбласти