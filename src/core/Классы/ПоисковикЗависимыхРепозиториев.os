#Использовать collectionos
#Использовать 1connector
#Использовать autumn-logos

&Деталька("litros.ТокенGitHub") 
Перем _Токен; // Строка

&Желудь
Процедура ПриСозданииОбъекта()
КонецПроцедуры

Функция НайтиРепозитории(ИмяПакета) Экспорт

	НайденныеРепозитории = Новый МножествоСоответствие();

	Если Не ЗначениеЗаполнено(ИмяПакета) Тогда
		Возврат НайденныеРепозитории;
	КонецЕсли;
	
	НайтиРепозиторииРекурсивно(ИмяПакета, 1, НайденныеРепозитории);

	Возврат НайденныеРепозитории;

КонецФункции

Процедура НайтиРепозиторииРекурсивно(ИмяПакета, НомерСтраницы, НайденныеРепозитории)
		
	МаксимальноеКоличествоНаСтранице = 100; // max 100

	Результат = ЗапроситьИспользованияПакетаВКоде(ИмяПакета, НомерСтраницы, МаксимальноеКоличествоНаСтранице);

	Если Результат["message"] <> Неопределено Тогда
		ТекстОшибки = СтрШаблон("Не удалось получить информацию об использовании пакета '%1': %2",
			ИмяПакета,
			Результат["message"]);
		ВызватьИсключение ТекстОшибки;
	КонецЕсли;

	ВсегоИспользований = Результат["total_count"];
	Если ВсегоИспользований = 0 Тогда
		Возврат;
	КонецЕсли;

	ЭтоНеполныйРезультат = Результат["incomplete_results"];
	Если ЭтоНеполныйРезультат Тогда
		ТекстОшибки = "Запрос к GitHub вернул неполные данные. Вероятно, достигнут лимит запросов.";
		ВызватьИсключение ТекстОшибки;
	КонецЕсли;

 	НайденныеЭлементы = Результат["items"];

	Для Каждого Использование Из НайденныеЭлементы Цикл

		АдресРепозитория = Использование["repository"]["html_url"];
		ЭтоПриватныйРепозиторий = Использование["repository"]["private"];

		Если ЭтоПриватныйРепозиторий Тогда
			Продолжить;
		КонецЕсли;

		НайденныеРепозитории.Добавить(АдресРепозитория);

	КонецЦикла;

	Если ВсегоИспользований = МаксимальноеКоличествоНаСтранице Тогда
		НайтиРепозиторииРекурсивно(ИмяПакета, НомерСтраницы + 1, НайденныеРепозитории);
	КонецЕсли;

КонецПроцедуры

Функция ЗапроситьИспользованияПакетаВКоде(ИмяПакета, НомерСтраницы, МаксимальноеКоличествоНаСтранице)

	СтрокаПоиска = """%1"" extension:os fork:true";

	Заголовки = Новый Соответствие();
	Заголовки.Вставить("Accept", "application/vnd.github+json");
	Заголовки.Вставить("X-GitHub-Api-Version", "2022-11-28");
	Заголовки.Вставить("Authorization", "Bearer " + _Токен);

	ДополнительныеПараметры = Новый Структура();
	ДополнительныеПараметры.Вставить("Заголовки", Заголовки);

	ПараметрыЗапроса = Новый Структура();
	ПараметрыЗапроса.Вставить("q", СтрШаблон(СтрокаПоиска, ТекстПоискаПоИспользованиюПакета(ИмяПакета)));
	ПараметрыЗапроса.Вставить("per_page", Формат(МаксимальноеКоличествоНаСтранице, "ЧГ="));
	ПараметрыЗапроса.Вставить("page", Формат(НомерСтраницы, "ЧГ="));

	Возврат КоннекторHTTP.Get("https://api.github.com/search/code", ПараметрыЗапроса, ДополнительныеПараметры).Json();

КонецФункции

Функция ТекстПоискаПоИспользованиюПакета(ИмяПакета)
	Возврат СтрШаблон("#Использовать %1", ИмяПакета);
КонецФункции