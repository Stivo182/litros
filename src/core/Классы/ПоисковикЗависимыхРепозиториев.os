#Использовать collectionos
#Использовать 1connector
#Использовать autumn-logos

&Деталька("litros.ТокенGitHub") 
Перем _Токен; // Строка

Перем _РегэксПодключенныхПакетов; // РегулярноеВыражение

&Желудь
Процедура ПриСозданииОбъекта()

	_РегэксПодключенныхПакетов = Новый РегулярноеВыражение("^\s*#(Использовать|Use)\s+([a-zA-Z0-9\-_]+).*");
	_РегэксПодключенныхПакетов.Многострочный = Истина;
	_РегэксПодключенныхПакетов.ИгнорироватьРегистр = Истина;

КонецПроцедуры

Функция НайтиРепозитории(ИмяПакета) Экспорт

	НайденныеРепозитории = Новый МножествоСоответствие();

	Если Не ЗначениеЗаполнено(ИмяПакета) Тогда
		Возврат НайденныеРепозитории;
	КонецЕсли;
	
	НайтиРепозиторииРекурсивно(ИмяПакета, 1, НайденныеРепозитории);

	Возврат НайденныеРепозитории;

КонецФункции

Процедура НайтиРепозиторииРекурсивно(ИмяПакета, НомерСтраницы, НайденныеРепозитории)
		
	МаксимальноеКоличествоНаСтранице = 100; // max 100

	Ответ = ЗапроситьИспользованияПакетаВКоде(ИмяПакета, НомерСтраницы, МаксимальноеКоличествоНаСтранице);
	Результат = Ответ.Json();
	ЕстьСледующаяСтраница = СтрНайти(Ответ.Заголовки["Link"], "rel=""next""") > 0;

	Если Результат["message"] <> Неопределено Тогда
		ТекстОшибки = СтрШаблон("Не удалось получить информацию об использовании пакета '%1': %2",
			ИмяПакета,
			Результат["message"]);
		ВызватьИсключение ТекстОшибки;
	КонецЕсли;

	ВсегоИспользований = Результат["total_count"];
	Если ВсегоИспользований = 0 Тогда
		Возврат;
	КонецЕсли;

	ЭтоНеполныйРезультат = Результат["incomplete_results"];
	Если ЭтоНеполныйРезультат Тогда
		ТекстОшибки = "Запрос к GitHub вернул неполные данные. Вероятно, достигнут лимит запросов.";
		ВызватьИсключение ТекстОшибки;
	КонецЕсли;

 	НайденныеЭлементы = Результат["items"];

	Для Каждого Элемент Из НайденныеЭлементы Цикл

		АдресРепозитория = Элемент["repository"]["html_url"];
		ЭтоПриватныйРепозиторий = Элемент["repository"]["private"];
		Совпадения = Элемент["text_matches"];

		Если ЭтоПриватныйРепозиторий Тогда
			Продолжить;
		КонецЕсли;

		НайденПакет = Ложь;
		Для Каждого Совпадение Из Совпадения Цикл
			Фрагмент = Совпадение["fragment"];
			Если ФрагментСодержитПодключениеПакета(Фрагмент, ИмяПакета) Тогда
				НайденПакет = Истина;
				Прервать;
			КонецЕсли;
		КонецЦикла;

		Если НайденПакет Тогда
			НайденныеРепозитории.Добавить(АдресРепозитория);
		КонецЕсли;

	КонецЦикла;

	Если ЕстьСледующаяСтраница Тогда
		НайтиРепозиторииРекурсивно(ИмяПакета, НомерСтраницы + 1, НайденныеРепозитории);
	КонецЕсли;

КонецПроцедуры

Функция ЗапроситьИспользованияПакетаВКоде(ИмяПакета, НомерСтраницы, МаксимальноеКоличествоНаСтранице)

	СтрокаПоиска = """%1"" extension:os fork:true";

	Заголовки = Новый Соответствие();
	Заголовки.Вставить("Accept", "application/vnd.github.text-match+json");
	Заголовки.Вставить("X-GitHub-Api-Version", "2022-11-28");
	Заголовки.Вставить("Authorization", "Bearer " + _Токен);

	ДополнительныеПараметры = Новый Структура();
	ДополнительныеПараметры.Вставить("Заголовки", Заголовки);

	ПараметрыЗапроса = Новый Структура();
	ПараметрыЗапроса.Вставить("q", СтрШаблон(СтрокаПоиска, ТекстПоискаПоИспользованиюПакета(ИмяПакета)));
	ПараметрыЗапроса.Вставить("per_page", Формат(МаксимальноеКоличествоНаСтранице, "ЧГ="));
	ПараметрыЗапроса.Вставить("page", Формат(НомерСтраницы, "ЧГ="));
	
	Возврат КоннекторHTTP.Get("https://api.github.com/search/code", ПараметрыЗапроса, ДополнительныеПараметры);

КонецФункции

Функция ТекстПоискаПоИспользованиюПакета(ИмяПакета)
	Возврат СтрШаблон("#Использовать %1", ИмяПакета);
КонецФункции

Функция ФрагментСодержитПодключениеПакета(Фрагмент, ИмяПакета)

	Совпадения = _РегэксПодключенныхПакетов.НайтиСовпадения(Фрагмент);
	Для Каждого Совпадение Из Совпадения Цикл	
		НайденноеИмяПакета = НРег(Совпадение.Группы[2].Значение);
		Если НайденноеИмяПакета = НРег(ИмяПакета) Тогда
			Возврат Истина;
		КонецЕсли;
	КонецЦикла;

	Возврат Ложь;

КонецФункции