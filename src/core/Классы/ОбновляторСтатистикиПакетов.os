#Использовать entity
#Использовать autumn-logos
#Использовать "../../dto"
#Использовать "../../internal"

&Пластилин("МенеджерСущностей")
Перем _МенеджерСущностей; // МенеджерСущностей (см. entity)

&Пластилин("ПоисковикЗависимыхРепозиториев")
Перем _ПоисковикЗависимыхРепозиториев; // ПоисковикЗависимыхРепозиториев

&Лог("litros.ОбновляторСтатистикиПакетов")
Перем _Лог; // Лог

&Деталька(Значение = "litros.ИнтервалПроверкиАктуальности", ЗначениеПоУмолчанию = 300) 
Перем _ИнтервалПроверкиАктуальности; // Число

&Деталька(Значение = "litros.СрокАктуальности", ЗначениеПоУмолчанию = 86400) 
Перем _СрокАктуальности; // Число

&Желудь
Процедура ПриСозданииОбъекта()
КонецПроцедуры

#Область ПрограммныйИнтерфейс

Функция ОбновитьПакет(ИмяПакета) Экспорт

	Результат = Новый Структура();
	Результат.Вставить("Успех", Ложь);
	Результат.Вставить("КоличествоОбратныхЗависимостей", 0);

	_Лог.Отладка("Обновление статистики пакета %1", ИмяПакета);

	НайденныеРепозитории = _ПоисковикЗависимыхРепозиториев.НайтиРепозитории(ИмяПакета);
	Если НайденныеРепозитории = Неопределено Тогда
		_Лог.Отладка("Не удалось обновить статистику пакета %1", ИмяПакета);
		Возврат Результат;
	КонецЕсли;

	КоличествоОбратныхЗависимостей = НайденныеРепозитории.Количество();
	
	ОпцииПоиска = Новый ОпцииПоиска();
	ОпцииПоиска.Отбор("ИмяПакета", ВидСравнения.Равно, ИмяПакета);

	СтатистикаПакета = _МенеджерСущностей.ПолучитьОдно(Тип("СущностьСтатистикаПакета"), ОпцииПоиска);
	Если СтатистикаПакета = Неопределено Тогда
		СтатистикаПакета = Новый СущностьСтатистикаПакета();
		СтатистикаПакета.ИмяПакета = ИмяПакета;
		СтатистикаПакета.ДатаДобавления = РаботаСДаннымиБД.НормализоватьУниверсальнуюДату(ТекущаяУниверсальнаяДата());
	КонецЕсли;

	СтатистикаПакета.ДатаОбновления = РаботаСДаннымиБД.НормализоватьУниверсальнуюДату(ТекущаяУниверсальнаяДата());
	СтатистикаПакета.КоличествоОбратныхЗависимостей = КоличествоОбратныхЗависимостей;
	
	_МенеджерСущностей.Сохранить(СтатистикаПакета);

	Результат.Успех = Истина;
	Результат.КоличествоОбратныхЗависимостей = КоличествоОбратныхЗависимостей;

	_Лог.Отладка("Успешное обновление статистики пакета %1", ИмяПакета);

	Возврат Результат;

КонецФункции

Процедура ОбновитьВсеПакеты() Экспорт
	
	РазмерВыборки = 10;

	_Лог.Отладка("Запуск обновления статистики всех пакетов");

	ДатаОкончания = ТекущаяУниверсальнаяДата() - _СрокАктуальности;
	ДатаОкончания = РаботаСДаннымиБД.НормализоватьУниверсальнуюДату(ДатаОкончания);

	ОпцииПоиска = Новый ОпцииПоиска();
	ОпцииПоиска.Первые(РазмерВыборки);
	ОпцииПоиска.Отбор("ДатаОбновления", ВидСравнения.МеньшеИлиРавно, ДатаОкончания);
	ОпцииПоиска.СортироватьПо("ДатаОбновления", НаправлениеСортировки.Возр);

	СтатистикаПакетов = _МенеджерСущностей.Получить(Тип("СущностьСтатистикаПакета"), ОпцииПоиска);

	Для Каждого СтатистикаПакета Из СтатистикаПакетов Цикл
		Попытка
			ОбновитьПакет(СтатистикаПакета.ИмяПакета);
		Исключение
			ТекстОшибки = СтрШаблон("Не удалось обновить статистику пакета '%1': 
			|%2", СтатистикаПакета.ИмяПакета, ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			_Лог.Ошибка(ТекстОшибки);
		КонецПопытки;
	КонецЦикла;

	_Лог.Отладка("Завершение обновления статистики всех пакетов");
	
КонецПроцедуры

Процедура ЗапуститьИнтервальноеОбновлениеПакетов() Экспорт

	_Лог.Отладка("Запуск интервального обновления статистики всех пакетов");
	
	Пока Истина Цикл

		Попытка
			ОбновитьВсеПакеты();
		Исключение
			ТекстОшибки = СтрШаблон("Не удалось обновить статистику пакетов: 
			|%1", ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			_Лог.Ошибка(ТекстОшибки);	
		КонецПопытки;

		Приостановить(_ИнтервалПроверкиАктуальности * 1000);

	КонецЦикла;

КонецПроцедуры

#КонецОбласти