#Использовать entity
#Использовать autumn-logos

&Пластилин("МенеджерСущностей")
Перем _МенеджерСущностей; // МенеджерСущностей (см. entity)

&Пластилин("ПоисковикЗависимыхРепозиториев")
Перем _ПоисковикЗависимыхРепозиториев; // ПоисковикЗависимыхРепозиториев

&Лог("litros.ОбновляторСтатистикиПакетов")
Перем _Лог; // Лог

&Деталька(Значение = "litros.ИнтервалПроверкиАктуальности", ЗначениеПоУмолчанию = 300) 
Перем _ИнтервалПроверкиАктуальности; // Число

&Деталька(Значение = "litros.СрокАктуальности", ЗначениеПоУмолчанию = 86400) 
Перем _СрокАктуальности; // Число

&Желудь
Процедура ПриСозданииОбъекта()
КонецПроцедуры

Процедура ОбновитьСтатистикуПакета(ИмяПакета) Экспорт

	НайденныеРепозитории = _ПоисковикЗависимыхРепозиториев.НайтиРепозитории(ИмяПакета);

	КоличествоОбратныхЗависимостей = НайденныеРепозитории.Количество();
	
	ОпцииПоиска = Новый ОпцииПоиска();
	ОпцииПоиска.Отбор("ИмяПакета", ВидСравнения.Равно, ИмяПакета);

	СтатистикаПакета = _МенеджерСущностей.ПолучитьОдно(Тип("СущностьСтатистикаПакета"), ОпцииПоиска);
	Если СтатистикаПакета = Неопределено Тогда
		СтатистикаПакета = Новый СущностьСтатистикаПакета();
		СтатистикаПакета.ИмяПакета = ИмяПакета;
		СтатистикаПакета.ДатаДобавления = РаботаСДаннымиБД.НормализоватьУниверсальнуюДату(ТекущаяУниверсальнаяДата());
	КонецЕсли;

	СтатистикаПакета.ДатаОбновления = РаботаСДаннымиБД.НормализоватьУниверсальнуюДату(ТекущаяУниверсальнаяДата());
	СтатистикаПакета.КоличествоОбратныхЗависимостей = КоличествоОбратныхЗависимостей;
	
	_МенеджерСущностей.Сохранить(СтатистикаПакета);

КонецПроцедуры

Процедура АктуализироватьСтатистикуПакетов() Экспорт
	
	ДатаОкончания = ТекущаяУниверсальнаяДата() - _СрокАктуальности;
	ДатаОкончания = РаботаСДаннымиБД.НормализоватьУниверсальнуюДату(ДатаОкончания);

	ОпцииПоиска = Новый ОпцииПоиска();
	ОпцииПоиска.Отбор("ДатаОбновления", ВидСравнения.МеньшеИлиРавно, ДатаОкончания);

	СтатистикаПакетов = _МенеджерСущностей.Получить(Тип("СущностьСтатистикаПакета"), ОпцииПоиска);

	Для Каждого СтатистикаПакета Из СтатистикаПакетов Цикл
		Попытка
			ОбновитьСтатистикуПакета(СтатистикаПакета.ИмяПакета);
		Исключение
			ТекстОшибки = СтрШаблон("Не удалось актуализировать статистику пакета '%1': 
			|%2", СтатистикаПакета.ИмяПакета, ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			_Лог.Ошибка(ТекстОшибки);
		КонецПопытки;
	КонецЦикла;
	
КонецПроцедуры

Процедура ЗапуститьПериодическуюАктуализацию() Экспорт
	
	Пока Истина Цикл
		Попытка
			АктуализироватьСтатистикуПакетов();
		Исключение
			ТекстОшибки = СтрШаблон("Не удалось актуализировать статистику пакетов: 
			|%1", ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			_Лог.Ошибка(ТекстОшибки);			
		КонецПопытки;

		Приостановить(_ИнтервалПроверкиАктуальности * 1000);
	КонецЦикла;

КонецПроцедуры