#Использовать collectionos
#Использовать autumn-logos

&Пластилин("КлиентЛимитированныхЗапросовHTTP")
Перем _КлиентЛимитированныхЗапросовHTTP; // КлиентЛимитированныхЗапросовHTTP

&Лог("litros.ПоисковикЗависимыхРепозиториев")
Перем _Лог; // Лог

&Деталька("litros.ТокенGitHub") 
Перем _Токен; // Строка

Перем _РегэксПодключенныхПакетов; // РегулярноеВыражение
Перем _НакопленныеДанныеПоискаРепозиториев; // СинхронизированнаяКарта

&Желудь
Процедура ПриСозданииОбъекта()

	_НакопленныеДанныеПоискаРепозиториев = Новый СинхронизированнаяКарта(Новый КартаСоответствие());

	_РегэксПодключенныхПакетов = Новый РегулярноеВыражение("^\s*#(Использовать|Use)\s+([a-zA-Z0-9\-_]+).*");
	_РегэксПодключенныхПакетов.Многострочный = Истина;
	_РегэксПодключенныхПакетов.ИгнорироватьРегистр = Истина;

КонецПроцедуры

#Область ПрограммныйИнтерфейс

// Выполняет поиск репозиториев на github.com, в которых используется пакет.
//
// Параметры:
//   ИмяПакета - Строка - Имя анализируемого пакета
//
// Возвращаемое значение:
//   Неопределено - Если произошла ошибка или превышены лимиты GitHub API.
//   Массив из Структура - Список найденных репозиториев:
//     * URL - Строка - URL репозитория
//     * ПолноеИмя - Строка - Полное имя репозитория
//     * Владелец - Строка - Логин владельца
//     * ЭтоФорк - Булево - Признак форка
Функция НайтиРепозитории(ИмяПакета) Экспорт

	НайденныеРепозитории = Новый КартаСоответствие();

	Если Не ЗначениеЗаполнено(ИмяПакета) Тогда
		Возврат НайденныеРепозитории;
	КонецЕсли;

	УдалитьУстаревшиеНакопленныеДанные();
	
	НакопленныеДанные = _НакопленныеДанныеПоискаРепозиториев.Получить(ИмяПакета);
	Если Не НакопленныеДанные.СодержитЗначение() Тогда
		НомерСтраницы = 1;
		ШагДиапазонаРазмераФайла = 0;
		ТолькоФорки = Ложь;
	Иначе
		НакопленныеДанные = НакопленныеДанные.Получить();
		НомерСтраницы = НакопленныеДанные["НомерСтраницы"] + 1;
		ШагДиапазонаРазмераФайла = НакопленныеДанные["ШагДиапазонаРазмераФайла"];
		ТолькоФорки = НакопленныеДанные["ТолькоФорки"];
		НайденныеРепозитории = НакопленныеДанные["Репозитории"];	
	КонецЕсли;

	_Лог.Отладка("Начат поиск репозиториев, зависящих от пакета %1
	|Номер страницы: %2, Шаг диапазона размера файла: %3, Только форки: %4, Найдено всего репозиториев: %5",
		ИмяПакета,
		НомерСтраницы,
		ШагДиапазонаРазмераФайла,
		ТолькоФорки,
		НайденныеРепозитории.Количество());

	РезультатПоиска = НайтиРепозиторииСоСтраницы(ИмяПакета,
		НомерСтраницы,
		ШагДиапазонаРазмераФайла,
		ТолькоФорки,
		НайденныеРепозитории);

	Если РезультатПоиска.Успех Тогда

		_НакопленныеДанныеПоискаРепозиториев.Удалить(ИмяПакета);

		_Лог.Отладка("Успешно завершён поиск репозиториев, зависящих от пакета %1. Обнаружено репозиториев: %2",
			ИмяПакета,
			НайденныеРепозитории.Количество());

		Возврат НайденныеРепозитории.Значения().ВМассив();

	КонецЕсли;

	НужноПоместитьВоВременноеХранилище = Не РезультатПоиска.Успех
		И (НомерСтраницы > 1 
			Или ШагДиапазонаРазмераФайла > 1 
			Или Не ТолькоФорки); // Форки запрашиваются последними
	
	Если НужноПоместитьВоВременноеХранилище Тогда

		НакопленныеДанные = Новый Структура();	
		НакопленныеДанные.Вставить("НомерСтраницы", НомерСтраницы);
		НакопленныеДанные.Вставить("ШагДиапазонаРазмераФайла", ШагДиапазонаРазмераФайла);
		НакопленныеДанные.Вставить("ТолькоФорки", ТолькоФорки);
		НакопленныеДанные.Вставить("Репозитории", НайденныеРепозитории);
		НакопленныеДанные.Вставить("Дата", ТекущаяУниверсальнаяДата());

		_НакопленныеДанныеПоискаРепозиториев.Вставить(ИмяПакета, НакопленныеДанные);

		_Лог.Отладка("Найденные репозитории, зависящие от пакета %1, помещены во временное хранилище для последующей обработки.
		|Номер страницы: %2, Шаг диапазона размера файла: %3, Только форки: %4, Найдено всего репозиториев: %5",
			ИмяПакета,
			НомерСтраницы,
			ШагДиапазонаРазмераФайла,
			ТолькоФорки,
			НайденныеРепозитории.Количество());

	КонецЕсли;

	Возврат Неопределено;

КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция НайтиРепозиторииСоСтраницы(ИмяПакета, НомерСтраницы, ШагДиапазонаРазмераФайла, ТолькоФорки, НайденныеРепозитории)

	Результат = Новый Структура();
	Результат.Вставить("Успех", Истина);

	Пока Истина Цикл

		РезультатПоиска = НайтиРепозиторииНаСтранице(ИмяПакета,
			НомерСтраницы,
			ШагДиапазонаРазмераФайла,
			ТолькоФорки,
			НайденныеРепозитории);
			
		Результат.Успех = РезультатПоиска.Успех;

		ПовыситьШагДиапазонаРазмераФайла = Результат.Успех 
			И РезультатПоиска.ЕстьДанные
			И Не РезультатПоиска.ЕстьСледующаяСтраница
			И ШагДиапазонаРазмераФайла > 0;

		ПерейтиНаФильтрациюПоРазмеруФайла = РезультатПоиска.ДостигнутМаксимумРезультатов И ШагДиапазонаРазмераФайла = 0;

		ПереключитьсяНаФорки = Не ТолькоФорки
			И Результат.Успех
			И Не РезультатПоиска.ЕстьСледующаяСтраница
			И Не ПовыситьШагДиапазонаРазмераФайла
			И Не ПерейтиНаФильтрациюПоРазмеруФайла;

		Если Результат.Успех И РезультатПоиска.ЕстьСледующаяСтраница Тогда

			НомерСтраницы = НомерСтраницы + 1;

			_Лог.Отладка("Переход на страницу %1 при поиске репозиториев, зависящих от пакета %2",
				НомерСтраницы,
				ИмяПакета);

		ИначеЕсли ПовыситьШагДиапазонаРазмераФайла Или ПерейтиНаФильтрациюПоРазмеруФайла Тогда

			НомерСтраницы = 1;
			ШагДиапазонаРазмераФайла = ШагДиапазонаРазмераФайла + 1;

			_Лог.Отладка("Увеличен шаг диапазона размера файла до %1 при поиске репозиториев, зависящих от пакета %2",
				ШагДиапазонаРазмераФайла,
				ИмяПакета);

		ИначеЕсли ПереключитьсяНаФорки Тогда

			ТолькоФорки = Истина;
			НомерСтраницы = 1;
			ШагДиапазонаРазмераФайла = 0;

		Иначе
			Прервать;
		КонецЕсли;

	КонецЦикла;

	Возврат Результат;

КонецФункции

Функция НайтиРепозиторииНаСтранице(ИмяПакета, НомерСтраницы, ШагДиапазонаРазмераФайла, ТолькоФорки, НайденныеРепозитории)
			
	МаксимальноеКоличествоНаВсехСтраницах = 1000;

	Результат = Новый Структура;
	Результат.Вставить("Успех", Ложь);
	Результат.Вставить("ЕстьДанные", Ложь);
	Результат.Вставить("ЕстьСледующаяСтраница", Ложь);
	Результат.Вставить("ДостигнутМаксимумРезультатов", Ложь);

	Ответ = НайтиИспользованияПакетаВКодеЧерезGitHub(ИмяПакета,
		НомерСтраницы,
		ШагДиапазонаРазмераФайла,
		ТолькоФорки);

	Если Ответ = Неопределено Тогда
		Возврат Результат;
	КонецЕсли;

	ДанныеОтвета = Ответ.Json();
	
	Если Ответ.КодСостояния <> 200 Тогда

		ТекстОшибки = СтрШаблон("Не удалось получить информацию об использовании пакета '%1': %2",
			ИмяПакета,
			ДанныеОтвета["message"]);
		_Лог.Ошибка(ТекстОшибки);

		Если ДанныеОтвета["message"] = "Cannot access beyond the first 1000 results" Тогда
			Результат.ДостигнутМаксимумРезультатов = Истина;
		КонецЕсли;

		Возврат Результат;

	КонецЕсли;

	КоличествоИспользований = ДанныеОтвета["total_count"];
	ЭтоНеполныйРезультат = ДанныеОтвета["incomplete_results"];
	НайденныеЭлементы = ДанныеОтвета["items"];

	Результат.ЕстьДанные = КоличествоИспользований > 0;
	Результат.ЕстьСледующаяСтраница = СтрНайти(Ответ.Заголовки["Link"], "rel=""next""") > 0;
	
	Если ЭтоНеполныйРезультат Тогда
		_Лог.Ошибка("Запрос к GitHub вернул неполные данные. Вероятно, достигнут лимит запросов.");
		Возврат Результат;
	КонецЕсли;
		
	Если КоличествоИспользований = 0 Тогда
		Результат.Успех = Истина;
		Возврат Результат;
	КонецЕсли;

	Если ШагДиапазонаРазмераФайла = 0 И КоличествоИспользований > МаксимальноеКоличествоНаВсехСтраницах Тогда
		Результат.ДостигнутМаксимумРезультатов = Истина;
		Возврат Результат;
	КонецЕсли;

	Для Каждого Элемент Из НайденныеЭлементы Цикл

		АдресРепозитория = Элемент["repository"]["html_url"];
		ЭтоПриватныйРепозиторий = Элемент["repository"]["private"];
		Совпадения = Элемент["text_matches"];

		Если ЭтоПриватныйРепозиторий Тогда
			Продолжить;
		КонецЕсли;

		НайденПакет = Ложь;
		Для Каждого Совпадение Из Совпадения Цикл
			Фрагмент = Совпадение["fragment"];
			Если ФрагментСодержитПодключениеПакета(Фрагмент, ИмяПакета) Тогда
				НайденПакет = Истина;
				Прервать;
			КонецЕсли;
		КонецЦикла;

		Если НайденПакет И Не НайденныеРепозитории.СодержитКлюч(АдресРепозитория) Тогда
			ОписаниеРепозитория = Новый Структура();
			ОписаниеРепозитория.Вставить("URL", АдресРепозитория);
			ОписаниеРепозитория.Вставить("ПолноеИмя", Элемент["repository"]["full_name"]);
			ОписаниеРепозитория.Вставить("Владелец", Элемент["repository"]["owner"]["login"]);
			ОписаниеРепозитория.Вставить("ЭтоФорк", Элемент["repository"]["fork"] = Истина);

			НайденныеРепозитории.Вставить(АдресРепозитория, ОписаниеРепозитория);
		КонецЕсли;

	КонецЦикла;
	
	Результат.Успех = Истина;

	Возврат Результат;

КонецФункции

Функция НайтиИспользованияПакетаВКодеЧерезGitHub(ИмяПакета, НомерСтраницы, ШагДиапазонаРазмераФайла, ТолькоФорки)

	МаксимальноеКоличествоНаСтранице = 100; // max 100
	ДиапазонРазмераФайла = 20000; // байт

	ШаблонПоиска = """%1"" extension:os %2 %3";
	ФильтрПоРазмеру = "";
	ФильтрПоФоркам = "";

	// Параметр fork:true в эндпоинте https://api.github.com/search/code предназначен 
	// для включения в результаты поиска кода из форков репозиториев в дополнение к оригинальным. 
	// Однако на практике поведение этого параметра нестабильно: в некоторых запросах оригинальные 
	// репозитории полностью исчезают из выдачи, и возвращаются только результаты из форков.
	Если ТолькоФорки Тогда
		ФильтрПоФоркам = "fork:only";
	КонецЕсли;

	Если ШагДиапазонаРазмераФайла > 0 Тогда
		ФильтрПоРазмеру = СтрШаблон("size:%1..%2", 
			Формат((ШагДиапазонаРазмераФайла - 1) * ДиапазонРазмераФайла, "ЧН=0; ЧГ=")
			Формат(ШагДиапазонаРазмераФайла * ДиапазонРазмераФайла, "ЧН=0; ЧГ="));
	КонецЕсли;

	СтрокаПоиска = СтрШаблон(ШаблонПоиска,
		ТекстПоискаПоИспользованиюПакета(ИмяПакета),
		ФильтрПоФоркам,
		ФильтрПоРазмеру);

	Заголовки = Новый Соответствие();
	Заголовки.Вставить("Accept", "application/vnd.github.text-match+json");
	Заголовки.Вставить("X-GitHub-Api-Version", "2022-11-28");
	Заголовки.Вставить("Authorization", "Bearer " + _Токен);

	ДополнительныеПараметры = Новый Структура();
	ДополнительныеПараметры.Вставить("Заголовки", Заголовки);

	ПараметрыЗапроса = Новый Структура();
	ПараметрыЗапроса.Вставить("q", СтрокаПоиска);
	ПараметрыЗапроса.Вставить("per_page", Формат(МаксимальноеКоличествоНаСтранице, "ЧГ="));
	ПараметрыЗапроса.Вставить("page", Формат(НомерСтраницы, "ЧГ="));

	Попытка
		Ответ = _КлиентЛимитированныхЗапросовHTTP.Get("https://api.github.com/search/code",
			ПараметрыЗапроса,
			ДополнительныеПараметры);
	Исключение
		_Лог.Ошибка(КраткоеПредставлениеОшибки(ИнформацияОбОшибке()));
		Возврат Неопределено;
	КонецПопытки;

	Возврат Ответ;

КонецФункции

Функция ТекстПоискаПоИспользованиюПакета(ИмяПакета)
	Возврат СтрШаблон("#Использовать %1", ИмяПакета);
КонецФункции

Функция ФрагментСодержитПодключениеПакета(Фрагмент, ИмяПакета)

	Совпадения = _РегэксПодключенныхПакетов.НайтиСовпадения(Фрагмент);
	Для Каждого Совпадение Из Совпадения Цикл	
		НайденноеИмяПакета = НРег(Совпадение.Группы[2].Значение);
		Если НайденноеИмяПакета = НРег(ИмяПакета) Тогда
			Возврат Истина;
		КонецЕсли;
	КонецЦикла;

	Возврат Ложь;

КонецФункции

Процедура УдалитьУстаревшиеНакопленныеДанные()

	СрокХранения = 86400;
	ТекущаяДата = ТекущаяУниверсальнаяДата();

	Контекст = Новый Структура();
	Контекст.Вставить("СрокХранения", СрокХранения);
	Контекст.Вставить("ТекущаяДата", ТекущаяДата);
	Контекст.Вставить("Коллекция", _НакопленныеДанныеПоискаРепозиториев);

	_НакопленныеДанныеПоискаРепозиториев.ДляКаждого("(Ключ, Значение) -> {
		|	Если Значение.Дата + СрокХранения < ТекущаяДата Тогда
		|		Коллекция.Удалить(Ключ);
		|	КонецЕсли;
		|}", Контекст);
	
КонецПроцедуры

#КонецОбласти