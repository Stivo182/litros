#Использовать collectionos
#Использовать entity
#Использовать autumn-logos

&Пластилин("МенеджерСущностей")
Перем _МенеджерСущностей; // МенеджерСущностей (см. entity)

&Пластилин("ОбновляторСтатистикиПакетов")
Перем _ОбновляторСтатистикиПакетов; // ОбновляторСтатистикиПакетов

&Лог("litros.ПроцессорОчередиПакетов")
Перем _Лог; // Лог

&Деталька(Значение = "litros.ИнтервалОбработкиОчереди", ЗначениеПоУмолчанию = 60) 
Перем _ИнтервалОбработкиОчереди; // Число

&Желудь
Процедура ПриСозданииОбъекта()
КонецПроцедуры

Процедура Запустить() Экспорт

	ОпцииПоиска = Новый ОпцииПоиска();
	ОпцииПоиска.СортироватьПо("ДатаДобавления", НаправлениеСортировки.Возр);

	Пока Истина Цикл

		Попытка
			ОчередьПакетов = _МенеджерСущностей.Получить(Тип("СущностьОчередьПакета"), ОпцииПоиска);
			Для Каждого ОчередьПакета Из ОчередьПакетов Цикл
				ОбработатьПакет(ОчередьПакета);
			КонецЦикла;
		Исключение
			ТекстОшибки = СтрШаблон("Возникла ошибка при обработке очереди пакетов: 
			|%1", ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			_Лог.Ошибка(ТекстОшибки);
		КонецПопытки;

		Приостановить(_ИнтервалОбработкиОчереди * 1000);

	КонецЦикла;

КонецПроцедуры

Процедура ДобавитьВОчередь(ИмяПакета) Экспорт
		
	ОпцииПоиска = Новый ОпцииПоиска();
	ОпцииПоиска.Отбор("ИмяПакета", ВидСравнения.Равно, ИмяПакета);

	ОчередьПакета = _МенеджерСущностей.ПолучитьОдно(Тип("СущностьОчередьПакета"), ОпцииПоиска);
	Если Не ОчередьПакета = Неопределено Тогда
		Возврат;
	КонецЕсли;

	ОчередьПакета = Новый СущностьОчередьПакета();
	ОчередьПакета.ИмяПакета = ИмяПакета;	
	ОчередьПакета.ДатаДобавления = РаботаСДаннымиБД.НормализоватьУниверсальнуюДату(ТекущаяУниверсальнаяДата());

	_МенеджерСущностей.Сохранить(ОчередьПакета);

КонецПроцедуры

Процедура ОбработатьПакет(ОчередьПакета)

	ИмяПакета = ОчередьПакета.ИмяПакета;

	Попытка
		_ОбновляторСтатистикиПакетов.ОбновитьСтатистикуПакета(ИмяПакета);	
		_МенеджерСущностей.Удалить(ОчередьПакета);
	Исключение
		ТекстОшибки = СтрШаблон("Не удалось обновить статистику пакета '%1': 
		|%2", ИмяПакета, ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		_Лог.Ошибка(ТекстОшибки);
	КонецПопытки;

КонецПроцедуры